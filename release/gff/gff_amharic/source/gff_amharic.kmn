c =====================Begin Identity Section===================================================
c 
c The Geʾez Frontier Foundation's mnemonic input method for Amharic script on US-QWERTY
c keyboards for SIL Keyman, compliant with Unicode 4.1 and later.
c 
c Keyman        :  http://www.keyman.com/
c Documentation :  https://help.keyman.com/keyboard/gff_amharic/2.0/gff_amharic.php
c Source        :  https://github.com/keymanapp/keyboards/tree/master/release/gff/gff_amharic
c License       :  https://opensource.org/licenses/MIT
c Bugs          :  https://github.com/keymanapp/keyboards/issues
c 
c Specification :  http://keyboards.ethiopic.org/specification/
c Other Info    :  http://keyboards.ethiopic.org/ , http://unicode.org/charts/
c 
store(&VERSION) '15.0'
store(&NAME) 'አማርኛ (Amharic)'
store(&COPYRIGHT) '© 2009-2022 Geʾez Frontier Foundation'
store(&Message) "This is an Amharic language mnemonic input method for Ethiopic script that requires Unicode 4.1 support."
store(&CapsAlwaysOff) '1'
store(&HotKey) '[CTRL ALT K_A]'
store(&BITMAP) 'gff_amharic.ico'
store(&VISUALKEYBOARD) 'gff_amharic.kvks'
store(&TARGETS) 'any'
store(&LAYOUTFILE) 'gff_amharic.keyman-touch-layout'
store(&KMW_EMBEDCSS) 'gff_amharic.css'
$keymanonly:  store(&mnemonicLayout) "1"
c 
c =====================End Identity Section=====================================================

c =====================Begin Data Section=======================================================

c The remainder of this document is formatted with a fixed width font.

c ---------------------Maps of Syllabic Forms---------------------------------------------------
c the following is universal (language neutral) these mappings can be overridden by
c language specific mappings
  store(ግዕዝ)  'ሀለሐመሠረሰሸቀበቨተቸኀነኘከኸወዐዘዠየደጀገጠጨጰጸፀፈፐ'
  store(ካዕብ)  'ሁሉሑሙሡሩሱሹቁቡቩቱቹኁኑኙኩኹዉዑዙዡዩዱጁጉጡጩጱጹፁፉፑ'
  store(ሣልስ)  'ሂሊሒሚሢሪሲሺቂቢቪቲቺኂኒኚኪኺዊዒዚዢዪዲጂጊጢጪጲጺፂፊፒ'
  store(ራብዕ)  'ሃላሓማሣራሳሻቃባቫታቻኃናኛካኻዋዓዛዣያዳጃጋጣጫጳጻፃፋፓ'
  store(ኃምስ)  'ሄሌሔሜሤሬሴሼቄቤቬቴቼኄኔኜኬኼዌዔዜዤዬዴጄጌጤጬጴጼፄፌፔ'
  store(ሳድስ)  'ህልሕምሥርስሽቅብቭትችኅንኝክኽውዕዝዥይድጅግጥጭጵጽፅፍፕ'
  store(ሳብዕ)  'ሆሎሖሞሦሮሶሾቆቦቮቶቾኆኖኞኮኾዎዖዞዦዮዶጆጎጦጮጶጾፆፎፖ'

c QWERTY keymappings
c
c These stores were introduced in v2.3 of the keyboard to avoid producing an
c (ignorable) masking error that began appearing with KM15 where '_' was used
c a place holder for keys in the ሳድስ list that were not directly typed. The
c maps below are for for the initial keystrike only.
c
  store(enKeys)  'hlHmrsxqbvtcnNkKwzZydjgTCPSfp'
  store(ሳድስKeys) 'ህልሕምርስሽቅብቭትችንኝክኽውዝዥይድጅግጥጭጵጽፍፕ'

c the following is Amharic specific
  store(ሌሎች፡keys) 'LMRXBVQWYDJGF'
  store(ሌሎች፡ሳድስ)  'ልምርሽብቭቅውይድጅግፍ'
c ---------------------End Maps of Syllabic Forms-----------------------------------------------

c ---------------------Maps for Vowels----------------------------------------------------------
  store(loneVowelKeys)  [T_ግዕዝ]'aAuUiIeEoO'[T_ኃምስ]
  store(loneVowelEthio) 'አአአኡኡኢኢእእኦኦኤ'
c ---------------------End Vowel Maps-----------------------------------------------------------

c --------------------Maps for Labiovelars------------------------------------------------------
  store(ሳድስ፡መደበኛ)     'ህልሕምሥርስሽቅብቭትችኅንኝክዝዥድጅግጥጭጵጽፅፍፕ'
  store(ካዕብ፡መደበኛ)     'ሁሉሑሙሡሩሱሹቁቡቩቱቹኁኑኙኩዙዡዱጁጉጡጩጱጹፁፉፑ'
  store(ዘመደ፡ራብዕ፡መደበኛ) 'ኋሏሗሟሧሯሷሿቋቧቯቷቿኋኗኟኳዟዧዷጇጓጧጯጷጿጿፏፗ'

c 'hhkqg'
  store(ካዕብ፡ልዩ)  'ሁኁኩቁጉ'
  store(ዘመደ፡ግዕዝ) 'ኈኈኰቈጐ'
  store(ዘመደ፡ካዕብ) 'ኍኍኵቍጕ'
  store(ዘመደ፡ሣልስ) 'ኊኊኲቊጒ'
  store(ዘመደ፡ራብዕ) 'ኋኋኳቋጓ'
  store(ዘመደ፡ኃምስ) 'ኌኌኴቌጔ'
c ---------------------End Labiovelars----------------------------------------------------------

c ---------------------Maps for Numbers---------------------------------------------------------
  store(ArabOnes) '23456789'
  store(ones)     '፪፫፬፭፮፯፰፱'
  store(tens)     '፳፴፵፶፷፸፹፺'
  store(arabNumbers) '123456789'
  store(ethNumbers)  '፩፪፫፬፭፮፯፰፱፲፳፴፵፶፷፸፹፺፻፼'
  store(arabNumbersWithZero) '0123456789'
  store(ColonOrComma) ':,'
  store(ethWordspaceOrComma) '፡፣'
c ---------------------End Numbers--------------------------------------------------------------

c ---------------------Maps of Keystrokes of Syllabic Forms-------------------------------------
$keymanonly: store(ግዕዝ-key)  'eEäÄéÉèÈêÊëË'
$keymanweb:  store(ግዕዝ-key)  'eE'[T_ግዕዝ]
$keymanonly: store(ካዕብ-key) 'uUùÙúÚûÛüÜ'
$keymanweb:  store(ካዕብ-key) 'uU'[T_ካዕብ]
$keymanonly: store(ሣልስ-key) 'iIìÌíÍîÎïÏ'
$keymanweb:  store(ሣልስ-key) 'iI'[T_ሣልስ]
$keymanonly: store(ራብዕ-key) 'aAàÀáÁâÂãÃåÅ'
$keymanweb:  store(ራብዕ-key) 'aA'[T_ራብዕ]
$keymanonly: store(ኃምስ-key) 'æÆ'
$keymanweb:  store(ኃምስ-key) [T_ኃምስ]
$keymanonly: store(ሳብዕ-key) 'oOòÒóÓôÔõÕöÖøØ'
$keymanweb:  store(ሳብዕ-key) 'oO'[T_ሳብዕ]
c ---------------------Maps of Keystrokes of Syllabic Forms-------------------------------------

store(touchVowels)  [T_ግዕዝ][T_ካዕብ][T_ሣልስ][T_ራብዕ][T_ኃምስ][T_ሳብዕ][T_WWA]
store(ሀ_ቤተሰብ) 'ሀሁሂሃሄሆ!'
store(ለ_ቤተሰብ) 'ለሉሊላሌሎሏ'
store(ሐ_ቤተሰብ) 'ሐሑሒሓሔሖሗ'
store(መ_ቤተሰብ) 'መሙሚማሜሞሟ'
store(ሠ_ቤተሰብ) 'ሠሡሢሣሤሦሧ'
store(ረ_ቤተሰብ) 'ረሩሪራሬሮሯ'
store(ሰ_ቤተሰብ) 'ሰሱሲሳሴሶሷ'
store(ሸ_ቤተሰብ) 'ሸሹሺሻሼሾኋ'
store(ቀ_ቤተሰብ) 'ቀቁቂቃቄቆ!'
store(ቐ_ቤተሰብ) 'ቐቑቒቓቔቖ!'
store(በ_ቤተሰብ) 'በቡቢባቤቦቧ'
store(ቨ_ቤተሰብ) 'ቨቩቪቫቬቮቧ'
store(ተ_ቤተሰብ) 'ተቱቲታቴቶቷ'
store(ቸ_ቤተሰብ) 'ቸቹቺቻቼቾቷ'
store(ኀ_ቤተሰብ) 'ኀኁኂኃኄኆ!'
store(ነ_ቤተሰብ) 'ነኑኒናኔኖኗ'
store(ኘ_ቤተሰብ) 'ኘኙኚኛኜኞኗ'
store(ከ_ቤተሰብ) 'ከኩኪካኬኮ!'
store(ኸ_ቤተሰብ) 'ኸኹኺኻኼኾ!'
store(ወ_ቤተሰብ) 'ወዉዊዋዌዎ!'
store(ዐ_ቤተሰብ) 'ዐዑዒዓዔዖ!'
store(ዘ_ቤተሰብ) 'ዘዙዚዛዜዞዟ'
store(ዠ_ቤተሰብ) 'ዠዡዢዣዤዦዟ'
store(የ_ቤተሰብ) 'የዩዪያዬዮ!'
store(ደ_ቤተሰብ) 'ደዱዲዳዴዶዷ'
store(ጀ_ቤተሰብ) 'ጀጁጂጃጄጆጓ'
store(ገ_ቤተሰብ) 'ገጉጊጋጌጎ!'
store(ጘ_ቤተሰብ) 'ጘጙጚጛጜጞ!'
store(ጠ_ቤተሰብ) 'ጠጡጢጣጤጦጧ'
store(ጨ_ቤተሰብ) 'ጨጩጪጫጬጮጧ'
store(ጰ_ቤተሰብ) 'ጰጱጲጳጴጶጷ'
store(ጸ_ቤተሰብ) 'ጸጹጺጻጼጾጿ'
store(ፀ_ቤተሰብ) 'ፀፁፂፃፄፆ!'
store(ፈ_ቤተሰብ) 'ፈፉፊፋፌፎፏ'
store(ፐ_ቤተሰብ) 'ፐፑፒፓፔፖፗ'
c store(𞟠_ቤተሰብ) '𞟠𞟡𞟢𞟣𞟤𞟥𞟦!'  for future



c =====================End Data Section=========================================================

c =====================Begin Functional Section=================================================
c 
store(&KEYBOARDVERSION) '3.1'

  begin Unicode > use(main)
  group(main) using keys

c ---------------------Input of Special Symbols-------------------------------------------------
c 
c The input section here is for rules that must precede other rules
c 
  + '_' > U+135F c gemination
  U+135F + '_' > '_'

  + any(ሌሎች፡keys) > index(ሌሎች፡ሳድስ,1)
  + any(enKeys)   > index(ሳድስKeys,1)

c ---------------------End Input of Special Symbols---------------------------------------------

c ---------------------Input of Double Strike Letters-------------------------------------------
  'ስ' + 's' > 'ሥ'
  'ህ' + 'h' > 'ኅ'
  'ጽ' + 'S' > 'ፅ'
c ---------------------End Input of Double Strike Letters---------------------------------------

c ---------------------Input of Lone Vowels-----------------------------------------------------
c 
c Alef-A Vowels as Vowels
  + any(loneVowelKeys) > index(loneVowelEthio,1)
  any(loneVowelEthio) + "'" > index(loneVowelEthio,1) deadkey(1)  

  'አ' + any(ግዕዝ-key) > 'ኧ'

c Alef-A Vowels as Regular Syllables
  'እ' + any(ካዕብ-key) > 'ኡ'
  'እ' + any(ሣልስ-key) > 'ኢ'
  'እ' + any(ራብዕ-key) > 'ኣ'
  'እ' + any(ኃምስ-key) > 'ኤ'
  'ኢ' + any(ግዕዝ-key) > 'ኤ'
  'እ' + any(ሳብዕ-key) > 'ኦ'

c Aynu-A Vowels as Regular Syllables
  platform("touch")   'እ' + [T_ግዕዝ] > 'አ'
  platform("desktop") 'እ' + any(ግዕዝ-key) > 'ዕ'
  c 'ዕ' + any(ግዕዝ-key) > 'ዐ'
  c 'ዕ' + any(ካዕብ-key) > 'ዑ'
  c 'ዕ' + any(ሣልስ-key) > 'ዒ'
  c 'ዕ' + any(ራብዕ-key) > 'ዓ'
   'ዒ' + any(ግዕዝ-key) > 'ዔ'
   'ኤ' + any(ኃምስ-key) > 'ዔ'
  c 'ዕ' + any(ሳብዕ-key) > 'ዖ'

c Aynu-A Vowels as Vowels
  'ኡ' + any(ካዕብ-key) > 'ዑ'
  'ኢ' + any(ሣልስ-key) > 'ዒ'
  'ኦ' + any(ሳብዕ-key) > 'ዖ'

c Completing the Amharic "a" chain:
  'አ' + any(ራብዕ-key) > 'ዓ'
  'ዓ' + any(ራብዕ-key) > 'ዐ'
  'ዐ' + any(ራብዕ-key) > 'ኣ'

c ---------------------End Input of Lone Vowels-------------------------------------------------

c ---------------------Input of Basic Syllables-------------------------------------------------
  any(ሳድስ) + any(ግዕዝ-key) > index(ግዕዝ,1)
  any(ሳድስ) + any(ካዕብ-key) > index(ካዕብ,1)
  any(ሳድስ) + any(ሣልስ-key) > index(ሣልስ,1)
  any(ሳድስ) + any(ራብዕ-key) > index(ራብዕ,1)
  any(ሳድስ) + any(ኃምስ-key) > index(ኃምስ,1)
  any(ሣልስ) + any(ግዕዝ-key) > index(ኃምስ,1)  
  any(ሳድስ) + any(ሳብዕ-key) > index(ሳብዕ,1)
c ---------------------End Input of Basic Syllables---------------------------------------------

c ---------------------Input of Labiovelars-----------------------------------------------------
  any(ካዕብ፡መደበኛ) + any(ራብዕ-key) > index(ዘመደ፡ራብዕ፡መደበኛ,1)
  any(ሳድስ፡መደበኛ) + [T_WWA] > index(ዘመደ፡ራብዕ፡መደበኛ,1)
  any(ካዕብ፡ልዩ)   + any(ግዕዝ-key) > index(ዘመደ፡ግዕዝ,1)
  any(ካዕብ፡ልዩ)   + any(ካዕብ-key) > index(ዘመደ፡ካዕብ,1)
  any(ካዕብ፡ልዩ)   + any(ሣልስ-key) > index(ዘመደ፡ሣልስ,1)
  any(ካዕብ፡ልዩ)   + any(ራብዕ-key) > index(ዘመደ፡ራብዕ,1)
  any(ካዕብ፡ልዩ)   + any(ኃምስ-key) > index(ዘመደ፡ኃምስ,1)
  any(ዘመደ፡ሣልስ)  + any(ግዕዝ-key) > index(ዘመደ፡ኃምስ,1)

c Alternative input to support confusion around the 'o' glypheme:
  'ኮ' + 'o' > 'ኰ'
  'ጎ' + 'o' > 'ጐ'

  'ኰ' + 'o' > 'ኮ'
  'ጐ' + 'o' > 'ጎ'
c ---------------------End Input of Labiovelars-------------------------------------------------

c ---------------------Input of Punctuation-----------------------------------------------------
c 
c The only dead key for entry of special characters and
c to handle the ge'ez + lone vowel problem:  ግእዝ and not ገዝ
c 
  any(ሳድስ)      + "'" > index(ሳድስ,1) deadkey(1)
$keymanonly: any(ሳድስ)      + [CTRL ' '] > index(ሳድስ,1) deadkey(1)
  any(ካዕብ፡መደበኛ) + "'" > index(ካዕብ፡መደበኛ,1) deadkey(1)
  any(ካዕብ፡ልዩ)   + "'" > index(ካዕብ፡ልዩ,1) deadkey(1)
  any(ዘመደ፡ሣልስ)  + "'" > index(ዘመደ፡ሣልስ,1) deadkey(1)             
  any(ሣልስ)      + "'" > index(ሣልስ,1) deadkey(1)


  
  if( &layer = "ሀ" ) + any(touchVowels) > index(ሀ_ቤተሰብ,2)
  if( &layer = "ለ" ) + any(touchVowels) > index(ለ_ቤተሰብ,2)
  if( &layer = "ሐ" ) + any(touchVowels) > index(ሐ_ቤተሰብ,2)
  if( &layer = "መ" ) + any(touchVowels) > index(መ_ቤተሰብ,2)
  if( &layer = "ሠ" ) + any(touchVowels) > index(ሠ_ቤተሰብ,2)
  if( &layer = "ሠ-extra" ) + any(touchVowels) > index(ሠ_ቤተሰብ,2)
  if( &layer = "ረ" ) + any(touchVowels) > index(ረ_ቤተሰብ,2)
  if( &layer = "ሰ" ) + any(touchVowels) > index(ሰ_ቤተሰብ,2)
  if( &layer = "ሸ" ) + any(touchVowels) > index(ሸ_ቤተሰብ,2)
  if( &layer = "ቀ" ) + any(touchVowels) > index(ቀ_ቤተሰብ,2)
  if( &layer = "ቐ" ) + any(touchVowels) > index(ቐ_ቤተሰብ,2)
  if( &layer = "ቐ-extra" ) + any(touchVowels) > index(ቐ_ቤተሰብ,2)
  if( &layer = "በ" ) + any(touchVowels) > index(በ_ቤተሰብ,2)
  if( &layer = "ቨ" ) + any(touchVowels) > index(ቨ_ቤተሰብ,2)
  if( &layer = "ቨ-extra" ) + any(touchVowels) > index(ቨ_ቤተሰብ,2)
  if( &layer = "ተ" ) + any(touchVowels) > index(ተ_ቤተሰብ,2)
  if( &layer = "ቸ" ) + any(touchVowels) > index(ቸ_ቤተሰብ,2)
  if( &layer = "ኀ" ) + any(touchVowels) > index(ኀ_ቤተሰብ,2)
  if( &layer = "ኀ-extra" ) + any(touchVowels) > index(ኀ_ቤተሰብ,2)
  if( &layer = "ነ" ) + any(touchVowels) > index(ነ_ቤተሰብ,2) 
  if( &layer = "ኘ" ) + any(touchVowels) > index(ኘ_ቤተሰብ,2) 
  if( &layer = "ከ" ) + any(touchVowels) > index(ከ_ቤተሰብ,2) 
  if( &layer = "ኸ" ) + any(touchVowels) > index(ኸ_ቤተሰብ,2)
  if( &layer = "ኸ-extra" ) + any(touchVowels) > index(ኸ_ቤተሰብ,2)
  if( &layer = "ዐ" ) + any(touchVowels) > index(ዐ_ቤተሰብ,2)
  if( &layer = "ወ" ) + any(touchVowels) > index(ወ_ቤተሰብ,2)
  if( &layer = "ዘ" ) + any(touchVowels) > index(ዘ_ቤተሰብ,2) 
  if( &layer = "ዠ" ) + any(touchVowels) > index(ዠ_ቤተሰብ,2)
  if( &layer = "ዠ-extra" ) + any(touchVowels) > index(ዠ_ቤተሰብ,2) 
  if( &layer = "የ" ) + any(touchVowels) > index(የ_ቤተሰብ,2)
  if( &layer = "ደ" ) + any(touchVowels) > index(ደ_ቤተሰብ,2)
  if( &layer = "ጀ" ) + any(touchVowels) > index(ጀ_ቤተሰብ,2)
  if( &layer = "ገ" ) + any(touchVowels) > index(ገ_ቤተሰብ,2)
  if( &layer = "ጘ" ) + any(touchVowels) > index(ጘ_ቤተሰብ,2)
  if( &layer = "ጘ-extra" ) + any(touchVowels) > index(ጘ_ቤተሰብ,2)
  if( &layer = "ጠ" ) + any(touchVowels) > index(ጠ_ቤተሰብ,2) 
  if( &layer = "ጨ" ) + any(touchVowels) > index(ጨ_ቤተሰብ,2)
  if( &layer = "ጰ" ) + any(touchVowels) > index(ጰ_ቤተሰብ,2) 
  if( &layer = "ጸ" ) + any(touchVowels) > index(ጸ_ቤተሰብ,2)
  if( &layer = "ፀ" ) + any(touchVowels) > index(ፀ_ቤተሰብ,2)
  if( &layer = "ፀ-extra" ) + any(touchVowels) > index(ፀ_ቤተሰብ,2)
  if( &layer = "ፈ" ) + any(touchVowels) > index(ፈ_ቤተሰብ,2)
  if( &layer = "ፐ" ) + any(touchVowels) > index(ፐ_ቤተሰብ,2)
  if( &layer = "ፐ-extra" ) + any(touchVowels) > index(ፐ_ቤተሰብ,2)
c if( &layer = "𞟠-extra" ) + any(touchVowels) > index(𞟠_ቤተሰብ,2) for future

+ ':'     > '፡'
$keymanonly: + [SHIFT ' '] > '፡'
  '፡' + ':' > '።'
  '፡' + '-' > '፦'
  '፡' + '+' > '፠'
  '፡' + '#' > '፨'  
  '።' + ':' > ':'
  ':' + ':' > '፡'  c makes circular  
  
      + ',' > '፣'
  '፣' + ',' > '፥'
  '፥' + ',' > ','
  ',' + ',' > '፣'  c makes circular

      + ';' > '፤'
  '፤' + ';' > ';'
  ';' + ';' > '፤'  c makes circular

      + '<' > '‹'
      + '>' > '›'
  '‹' + '<' > '«'
  '›' + '>' > '»'
  '«' + '<' > '<'
  '»' + '>' > '>'
c ---------------------End Input of Punctuation-------------------------------------------------

c ---------------------Input of European Characters---------------------------------------------
$keymanonly:  store(euro)    'ÇçÐðÝýÿßÑñ'
$keymanonly:  store(euroMap) 'ጭችድድይይይጽኝኝ'
$keymanonly:  + any(euro) > index(euroMap,1)

$keymanonly:  'ጽ' + 'ß' > 'ፅ'
$keymanonly:  + '¨' > U+135F

$keymanonly:  store(euroVowels) 'àÀáÁâÂãÃåÅùÙúÚûÛüÜìÌíÍîÎïÏæÆéÉèÈêÊëËòÒóÓôÔõÕöÖøØ'
c  this mapping has an Amharic bias
$keymanonly:  store(euroVowelsMap) 'አአአአአአአአአአኡኡኡኡኡኡኡኡኢኢኢኢኢኢኢኢኤኤእእእእእእእእኦኦኦኦኦኦኦኦኦኦኦኦ'
$keymanonly:  + any(euroVowels) > index(euroVowelsMap,1)
c ---------------------End Input of European Characters-----------------------------------------

c ---------------------Input of Numbers---------------------------------------------------------

c Special Rule for Arabic Numerals
c 
c The following attempts to auto-correct the use of Ethiopic wordspace and
c Ethiopic comma within an Arabic numeral context.  Ethiopic wordspace gets
c used erroneously in time formats and Ethiopic commas as an order of thousands
c delimiter. The correction context is not known until numerals appear on _both_
c sides of the punctuation.
c 
  any(arabNumbersWithZero) any(ethWordspaceOrComma) + any(arabNumbers) > index(arabNumbersWithZero,1) index(ColonOrComma,2) index(arabNumbers,3)

c Ethiopic Numerals

  '#' + '1' > '፩'
  '#' + any(ArabOnes) > index(ones,2)

c special cases for multiples of one
    '፩' + '0' >   '፲'
    '፲' + '0' >   '፻'
    '፻' + '0' >  '፲፻'
   '፲፻' + '0' >   '፼'
    '፼' + '0' >  '፲፼'    
   '፲፼' + '0' >  '፻፼' 
   '፻፼' + '0' > '፲፻፼'
  '፲፻፼' + '0' >  '፼፼'
   '፼፼' + '0' > context beep  c do not go any higher, we could beep here

c upto the order of 100 million
  any(ones)      + '0' > index(tens,1)
  any(tens)      + '0' > index(ones,1)  '፻' c Hundreds
  any(ones)  '፻' + '0' > index(tens,1)  '፻' c Thousands
  any(tens)  '፻' + '0' > index(ones,1)  '፼' c Ten Thousands
  any(ones)  '፼' + '0' > index(tens,1)  '፼' c Hundred Thousands
  any(tens)  '፼' + '0' > index(ones,1) '፻፼' c Millions
  any(ones) '፻፼' + '0' > index(tens,1) '፻፼' c Ten Millions
  any(tens) '፻፼' + '0' > index(ones,1) '፼፼' c Hundred Millions

c enhance this later, look for something that can copy a match over
  any(ethNumbers) + any(arabNumbers) > index(ethNumbers,1)  index(ethNumbers,2)

+ [T_THOUSAND] > '፲፻'
c ---------------------End Input of Numbers-----------------------------------------------------

c ---------------------Extensions for Touch Layout----------------------------------------------
c
c For Amharic, extensions are Tigrinya and Xamtanga.
c These keys cannot be entered via a physical keyboard, and only in an upper layer of a touch layout.

  store(TouchExtended፡ግዕዝ)  'ቐጘ'
  store(TouchExtended፡ካዕብ)  'ቑጙ'
  store(TouchExtended፡ሣልስ)  'ቒጚ'
  store(TouchExtended፡ራብዕ)  'ቓጛ'
  store(TouchExtended፡ኃምስ)  'ቔጜ'
  store(TouchExtended፡ሳድስ)  'ቕጝ'
  store(TouchExtended፡ሳብዕ)  'ቖጞ'

  any(TouchExtended፡ሳድስ) + any(ግዕዝ-key) > index(TouchExtended፡ግዕዝ,1)
  any(TouchExtended፡ሳድስ) + any(ካዕብ-key) > index(TouchExtended፡ካዕብ,1)
  any(TouchExtended፡ሳድስ) + any(ሣልስ-key) > index(TouchExtended፡ሣልስ,1)
  any(TouchExtended፡ሳድስ) + any(ራብዕ-key) > index(TouchExtended፡ራብዕ,1)
  any(TouchExtended፡ሳድስ) + any(ኃምስ-key) > index(TouchExtended፡ኃምስ,1)
  any(TouchExtended፡ሣልስ) + any(ግዕዝ-key) > index(TouchExtended፡ኃምስ,1)  
  any(TouchExtended፡ሳድስ) + any(ሳብዕ-key) > index(TouchExtended፡ሳብዕ,1)
c ---------------------End Input of Basic Syllables---------------------------------------------

c ---------------------Input of Labiovelars-----------------------------------------------------
  store(TouchExtended፡ሳድስ፡መደበኛ)     'ቕኽጝ'
  store(TouchExtended፡ካዕብ፡መደበኛ)     'ቑኹጙ'
  store(TouchExtended፡ዘመደ፡ራብዕ፡መደበኛ) 'ቛዃጟ'

c 'QKG'
  store(TouchExtended፡ካዕብ፡ልዩ)  'ቑኹጙ'
  store(TouchExtended፡ዘመደ፡ግዕዝ) 'ቘዀⶓ'
  store(TouchExtended፡ዘመደ፡ካዕብ) 'ቝዅⶖ'
  store(TouchExtended፡ዘመደ፡ሣልስ) 'ቚዂⶔ'
  store(TouchExtended፡ዘመደ፡ራብዕ) 'ቛዃጟ'
  store(TouchExtended፡ዘመደ፡ኃምስ) 'ቜዄⶕ'

  any(TouchExtended፡ካዕብ፡መደበኛ) + any(ራብዕ-key) > index(TouchExtended፡ዘመደ፡ራብዕ፡መደበኛ,1)
  any(TouchExtended፡ካዕብ፡ልዩ)   + any(ግዕዝ-key) > index(TouchExtended፡ዘመደ፡ግዕዝ,1)
  any(TouchExtended፡ካዕብ፡ልዩ)   + any(ካዕብ-key) > index(TouchExtended፡ዘመደ፡ካዕብ,1)
  any(TouchExtended፡ካዕብ፡ልዩ)   + any(ሣልስ-key) > index(TouchExtended፡ዘመደ፡ሣልስ,1)
  any(TouchExtended፡ካዕብ፡ልዩ)   + any(ራብዕ-key) > index(TouchExtended፡ዘመደ፡ራብዕ,1)
  any(TouchExtended፡ካዕብ፡ልዩ)   + any(ኃምስ-key) > index(TouchExtended፡ዘመደ፡ኃምስ,1)
  any(TouchExtended፡ዘመደ፡ሣልስ)  + any(ግዕዝ-key) > index(TouchExtended፡ዘመደ፡ኃምስ,1)
c ---------------------Extensions for Touch Layout----------------------------------------------


c =====================End Functional Section===================================================
