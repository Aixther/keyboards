c =====================Begin Identity Section===================================================
c 
c The Geʾez Frontier Foundation's mnemonic input method for Harari script on US-QWERTY
c keyboards for SIL Keyman, compliant with Unicode 3.0 and later.
c 
c Keyman        :  http://www.keyman.com/
c Documentation :  https://help.keyman.com/keyboard/gff_amharic/2.0/gff_amharic.php
c Source        :  https://github.com/keymanapp/keyboards/tree/master/release/gff/gff_amharic
c License       :  https://opensource.org/licenses/MIT
c Bugs          :  https://github.com/keymanapp/keyboards/issues
c 
c Specification :  http://keyboards.ethiopic.org/specification/
c Other Info    :  http://keyboards.ethiopic.org/ , http://unicode.org/charts/
c 
store(&VERSION) '15.0'
store(&KEYBOARDVERSION) '1.0'
store(&NAME) 'ሐረሪ (Harari)'
store(&COPYRIGHT) '© 2023 Geʾez Frontier Foundation'
store(&Message) "This is a Harari language mnemonic input method for Ethiopic script that requires Unicode 3.0 support."
store(&CapsAlwaysOff) '1'
store(&HotKey) '[CTRL ALT K_H]'
store(&BITMAP) 'gff_harari.ico'
store(&VISUALKEYBOARD) 'gff_harari.kvks'
store(&TARGETS) 'any'
store(&LAYOUTFILE) 'gff_harari.keyman-touch-layout'
store(&KMW_EMBEDCSS) 'gff_harari.css'
$keymanonly:  store(&mnemonicLayout) "1"
c 
c =====================End Identity Section=====================================================

c =====================Begin Data Section=======================================================

c The remainder of this document is formatted with a fixed width font.

c Define the set of Ethiopic characters to support contest based input:
c
store(Ethiopic) 'ሀ' .. '፼' .. 'ᎀ' .. '᎙ⶀ' .. 'ⷞꬁ' .. 'ꬮ𞟠' .. '𞟾'

c ---------------------Maps of Syllabic Forms---------------------------------------------------
c the following is universal (language neutral) these mappings can be overridden by
c language specific mappings
  store(ግዕዝ)  'ሀለሐ𞟠መሠረሰሸቀቐበቨተቸኀነኘከኸወዘዠየደዸጀገጘጠጨጰጸፀፈፐ'
  store(ካዕብ)  'ሁሉሑ𞟡ሙሡሩሱሹቁቑቡቩቱቹኁኑኙኩኹዉዙዡዩዱዹጁጉጙጡጩጱጹፁፉፑ'
  store(ሣልስ)  'ሂሊሒ𞟢ሚሢሪሲሺቂቒቢቪቲቺኂኒኚኪኺዊዚዢዪዲዺጂጊጚጢጪጲጺፂፊፒ'
  store(ራብዕ)  'ሃላሓ𞟣ማሣራሳሻቃቓባቫታቻኃናኛካኻዋዛዣያዳዻጃጋጛጣጫጳጻፃፋፓ'
  store(ኃምስ)  'ሄሌሔ𞟤ሜሤሬሴሼቄቔቤቬቴቼኄኔኜኬኼዌዜዤዬዴዼጄጌጜጤጬጴጼፄፌፔ'
  store(ሳድስ)  'ህልሕ𞟥ምሥርስሽቅቕብቭትችኅንኝክኽውዝዥይድዽጅግጝጥጭጵጽፅፍፕ'
  store(ሳብዕ)  'ሆሎሖ𞟦ሞሦሮሶሾቆቖቦቮቶቾኆኖኞኮኾዎዞዦዮዶዾጆጎጞጦጮጶጾፆፎፖ'

c QWERTY keymappings
c
c These stores were introduced in v2.3 of the keyboard to avoid producing an
c (ignorable) masking error that began appearing with KM15 where '_' was used
c a place holder for keys in the ሳድስ list that were not directly typed. The
c maps below are for for the initial keystrike only.
c
  store(enKeys)  'hlHmrsxqbvtcnNkKwzZydjgTCPSfp'
  store(ሳድስKeys) 'ህልሕምርስሽቅብቭትችንኝክኽውዝዥይድጅግጥጭጵጽፍፕ'

c the following is Amharic specific
  store(ሌሎች፡keys) 'LMRXBVQWYDJGF'
  store(ሌሎች፡ሳድስ)  'ልምርሽብቭቅውይድጅግፍ'
c ---------------------End Maps of Syllabic Forms-----------------------------------------------

c ---------------------Maps for Vowels----------------------------------------------------------
  store(loneVowelKeys)  [T_ግዕዝ]'aAuUiIeEoO'[T_ኃምስ]
  store(loneVowelEthio) 'አአዐኡዑኢዒእዕኦዖኤ'
c ---------------------End Vowel Maps-----------------------------------------------------------

c --------------------Maps for Labiovelars------------------------------------------------------
  store(ሳድስ፡መደበኛ)     'ህልሕምሥርስሽቅብቭትችኅንኝክዝዥድጅግጥጭጵጽፅፍፕ'
  store(ካዕብ፡መደበኛ)     'ሁሉሑሙሡሩሱሹቁቡቩቱቹኁኑኙኩዙዡዱጁጉጡጩጱጹፁፉፑ'
  store(ዘመደ፡ራብዕ፡መደበኛ) 'ኋሏሗሟሧሯሷሿቋቧቯቷቿኋኗኟኳዟዧዷጇጓጧጯጷጿጿፏፗ'

c 'hhkqg'
  store(ካዕብ፡ልዩ)  'ሁኁኩቁቑጉጙ'
  store(ዘመደ፡ግዕዝ) 'ኈኈኰቈቘጐⶓ'
  store(ዘመደ፡ካዕብ) 'ኍኍኵቍቝጕⶖ'
  store(ዘመደ፡ሣልስ) 'ኊኊኲቊቚጒⶔ'
  store(ዘመደ፡ራብዕ) 'ኋኋኳቋቛጓጟ'
  store(ዘመደ፡ኃምስ) 'ኌኌኴቌቜጔⶕ'
c ---------------------End Labiovelars----------------------------------------------------------

c ---------------------Maps for Numbers---------------------------------------------------------
  store(ArabOnes) '23456789'
  store(ones)     '፪፫፬፭፮፯፰፱'
  store(tens)     '፳፴፵፶፷፸፹፺'
  store(arabNumbers) '123456789'
  store(ethNumbers)  '፩፪፫፬፭፮፯፰፱፲፳፴፵፶፷፸፹፺፻፼'
  store(arabNumbersWithZero) '0123456789'
  store(ColonOrComma) ':,'
  store(ethWordspaceOrComma) '፡፣'
c ---------------------End Numbers--------------------------------------------------------------

c ---------------------Maps of Keystrokes of Syllabic Forms-------------------------------------
$keymanonly: store(ግዕዝ-key)  'eEäÄéÉèÈêÊëË'
$keymanweb:  store(ግዕዝ-key)  'eE'
$keymanonly: store(ካዕብ-key) 'uUùÙúÚûÛüÜ'
$keymanweb:  store(ካዕብ-key) 'uU'
$keymanonly: store(ሣልስ-key) 'iIìÌíÍîÎïÏ'
$keymanweb:  store(ሣልስ-key) 'iI'
$keymanonly: store(ራብዕ-key) 'aAàÀáÁâÂãÃåÅ'
$keymanweb:  store(ራብዕ-key) 'aA'
$keymanonly: store(ኃምስ-key) 'æÆ'
c $keymanweb:  store(ኃምስ-key) [T_ኃምስ]
$keymanonly: store(ሳብዕ-key) 'oOòÒóÓôÔõÕöÖøØ'
$keymanweb:  store(ሳብዕ-key) 'oO'
c ---------------------Maps of Keystrokes of Syllabic Forms-------------------------------------

store(touchVowels)  [T_ግዕዝ][T_ካዕብ][T_ሣልስ][T_ራብዕ][T_ኃምስ][T_ሳብዕ][T_WWA]
store(ሀ_ቤተሰብ) 'ሀሁሂሃሄሆ!'
store(ለ_ቤተሰብ) 'ለሉሊላሌሎሏ'
store(ሐ_ቤተሰብ) 'ሐሑሒሓሔሖሗ'
store(መ_ቤተሰብ) 'መሙሚማሜሞሟ'
store(ሠ_ቤተሰብ) 'ሠሡሢሣሤሦሧ'
store(ረ_ቤተሰብ) 'ረሩሪራሬሮሯ'
store(ሰ_ቤተሰብ) 'ሰሱሲሳሴሶሷ'
store(ሸ_ቤተሰብ) 'ሸሹሺሻሼሾሿ'
store(ቀ_ቤተሰብ) 'ቀቁቂቃቄቆ!'
store(ቐ_ቤተሰብ) 'ቐቑቒቓቔቖ!'
store(በ_ቤተሰብ) 'በቡቢባቤቦቧ'
store(ቨ_ቤተሰብ) 'ቨቩቪቫቬቮቯ'
store(ተ_ቤተሰብ) 'ተቱቲታቴቶቷ'
store(ቸ_ቤተሰብ) 'ቸቹቺቻቼቾቿ'
store(ኀ_ቤተሰብ) 'ኀኁኂኃኄኆ!'
store(ነ_ቤተሰብ) 'ነኑኒናኔኖኗ'
store(ኘ_ቤተሰብ) 'ኘኙኚኛኜኞኟ'
store(አ_ቤተሰብ) 'አኡኢኣኤኦኧ'
store(ከ_ቤተሰብ) 'ከኩኪካኬኮ!'
store(ኸ_ቤተሰብ) 'ኸኹኺኻኼኾ!'
store(ወ_ቤተሰብ) 'ወዉዊዋዌዎ!'
store(ዐ_ቤተሰብ) 'ዐዑዒዓዔዖ!'
store(ዘ_ቤተሰብ) 'ዘዙዚዛዜዞዟ'
store(ዠ_ቤተሰብ) 'ዠዡዢዣዤዦዧ'
store(የ_ቤተሰብ) 'የዩዪያዬዮ!'
store(ደ_ቤተሰብ) 'ደዱዲዳዴዶዷ'
store(ዸ_ቤተሰብ) 'ዸዹዺዻዼዽዾዿ'
store(ጀ_ቤተሰብ) 'ጀጁጂጃጄጆጇ'
store(ገ_ቤተሰብ) 'ገጉጊጋጌጎ!'
store(ጘ_ቤተሰብ) 'ጘጙጚጛጜጞ!'
store(ጠ_ቤተሰብ) 'ጠጡጢጣጤጦጧ'
store(ጨ_ቤተሰብ) 'ጨጩጪጫጬጮጯ'
store(ጰ_ቤተሰብ) 'ጰጱጲጳጴጶጷ'
store(ጸ_ቤተሰብ) 'ጸጹጺጻጼጾጿ'
store(ፀ_ቤተሰብ) 'ፀፁፂፃፄፆ!'
store(ፈ_ቤተሰብ) 'ፈፉፊፋፌፎፏ'
store(ፐ_ቤተሰብ) 'ፐፑፒፓፔፖፗ'
store(𞟠_ቤተሰብ) '𞟠𞟡𞟢𞟣𞟤𞟥𞟦!'

store(touchVowelsLabialvelar) [T_ግዕዝ][T_ካዕብ][T_ሣልስ][T_ራብዕ][T_ኃምስ]
store(ቈ_ቤተሰብ) 'ቈቍቊቋቌ'
store(ቘ_ቤተሰብ) 'ቘቝቚቛቜ'
store(ኈ_ቤተሰብ) 'ኈኍኊኋኌ'
store(ኰ_ቤተሰብ) 'ኰኵኲኳኴ'
store(ዀ_ቤተሰብ) 'ዀዅዂዃዄ'
store(ጐ_ቤተሰብ) 'ጐጕጒጓጔ'
store(ⶓ_ቤተሰብ) 'ⶓⶖⶔጟⶕ'

c =====================End Data Section=========================================================

c =====================Begin Functional Section=================================================
c 
  begin Unicode > use(main)
  group(main) using keys

c ---------------------Input of Special Symbols-------------------------------------------------

$keymanonly: + [CTRL '.'] > U+135E
$keymanonly: U+135E + [CTRL '.'] > U+135F
$keymanonly: U+135F + [CTRL '.'] > U+135D
c
c Ethiopic combining marks for vowel and consonant stress.
c The use of T_ codes here is done to avoid the warning: 
c   "Hint: 109A The rule will never be matched for key '፞' because its key code is never fired."
c which occurs if the keys emit U_ identifiers.
c
+ [T_135D] > U+135D
+ [T_135E] > U+135E
+ [T_135F] > U+135F
c
c The following can happen from the touch keyboards፡
c
U+135E + [T_135E] > U+135F c 1 + 1 > 2
U+135E + [T_135F] > U+135D c 1 + 2 > 3
U+135F + [T_135E] > U+135D c 2 + 1 > 3
c
c warn but don't combine:
c
U+135E + [T_135D] > U+135E beep  c 1 + 3
U+135F + [T_135F] > U+135F beep  c 2 + 2
U+135F + [T_135D] > U+135F beep  c 2 + 3
U+135D + [T_135E] > U+135D beep  c 3 + 1
U+135D + [T_135F] > U+135D beep  c 3 + 2
U+135D + [T_135D] > U+135D beep  c 3 + 3

  + any(ሌሎች፡keys) > index(ሌሎች፡ሳድስ,1)
  + any(enKeys)   > index(ሳድስKeys,1)
c ---------------------End Input of Special Symbols---------------------------------------------

c ---------------------Begin Mobile Input ------------------------------------------------------
c
c Mobile input is separated from Desktop in v3.1 for ease of maintenance
c

  any(ሳድስ) + [T_ግዕዝ] > index(ግዕዝ,1)
  any(ሳድስ) + [T_ካዕብ] > index(ካዕብ,1)
  any(ሳድስ) + [T_ሣልስ] > index(ሣልስ,1)
  any(ሳድስ) + [T_ራብዕ] > index(ራብዕ,1)
  any(ሳድስ) + [T_ኃምስ] > index(ኃምስ,1) 
  any(ሳድስ) + [T_ሳብዕ] > index(ሳብዕ,1)
  any(ሳድስ፡መደበኛ) + [T_WWA] > index(ዘመደ፡ራብዕ፡መደበኛ,1)

  any(ዘመደ፡ራብዕ) +  [T_ግዕዝ] > index(ዘመደ፡ግዕዝ,1)
  any(ዘመደ፡ራብዕ) +  [T_ሣልስ] > index(ዘመደ፡ሣልስ,1)
  any(ዘመደ፡ራብዕ) +  [T_ራብዕ] > index(ዘመደ፡ራብዕ,1)
  any(ዘመደ፡ራብዕ) +  [T_ኃምስ] > index(ዘመደ፡ኃምስ,1)
  any(ዘመደ፡ራብዕ) +  [T_ካዕብ] > index(ዘመደ፡ካዕብ,1)
  
  any(ካዕብ፡ልዩ) +  [T_ግዕዝ] > index(ዘመደ፡ግዕዝ,1)
  any(ካዕብ፡ልዩ) +  [T_ሣልስ] > index(ዘመደ፡ሣልስ,1)
  any(ካዕብ፡ልዩ) +  [T_ራብዕ] > index(ዘመደ፡ራብዕ,1)
  any(ካዕብ፡ልዩ) +  [T_ኃምስ] > index(ዘመደ፡ኃምስ,1)
  any(ካዕብ፡ልዩ) +  [T_ካዕብ] > index(ዘመደ፡ካዕብ,1)

  
  if( &layer = "ሀ-layer" ) + any(touchVowels) > index(ሀ_ቤተሰብ,2)
  if( &layer = "ለ-layer" ) + any(touchVowels) > index(ለ_ቤተሰብ,2)
  if( &layer = "ሐ-layer" ) + any(touchVowels) > index(ሐ_ቤተሰብ,2)
  if( &layer = "መ-layer" ) + any(touchVowels) > index(መ_ቤተሰብ,2)
  if( &layer = "ሠ-layer" ) + any(touchVowels) > index(ሠ_ቤተሰብ,2)
c  if( &layer = "ሠ-extra" ) + any(touchVowels) > index(ሠ_ቤተሰብ,2)
  if( &layer = "ረ-layer" ) + any(touchVowels) > index(ረ_ቤተሰብ,2)
  if( &layer = "ሰ-layer" ) + any(touchVowels) > index(ሰ_ቤተሰብ,2)
  if( &layer = "ሸ-layer" ) + any(touchVowels) > index(ሸ_ቤተሰብ,2)
  if( &layer = "ቀ-layer" ) + any(touchVowels) > index(ቀ_ቤተሰብ,2)
c  if( &layer = "ቐ-layer" ) + any(touchVowels) > index(ቐ_ቤተሰብ,2)
  if( &layer = "ቐ-extra" ) + any(touchVowels) > index(ቐ_ቤተሰብ,2)
  if( &layer = "በ-layer" ) + any(touchVowels) > index(በ_ቤተሰብ,2)
  if( &layer = "ቨ-layer" ) + any(touchVowels) > index(ቨ_ቤተሰብ,2)
c  if( &layer = "ቨ-extra" ) + any(touchVowels) > index(ቨ_ቤተሰብ,2)
  if( &layer = "ተ-layer" ) + any(touchVowels) > index(ተ_ቤተሰብ,2)
  if( &layer = "ቸ-layer" ) + any(touchVowels) > index(ቸ_ቤተሰብ,2)
  if( &layer = "ኀ-layer" ) + any(touchVowels) > index(ኀ_ቤተሰብ,2)
c  if( &layer = "ኀ-extra" ) + any(touchVowels) > index(ኀ_ቤተሰብ,2)
  if( &layer = "ነ-layer" ) + any(touchVowels) > index(ነ_ቤተሰብ,2) 
  if( &layer = "ኘ-layer" ) + any(touchVowels) > index(ኘ_ቤተሰብ,2)
  if( &layer = "default" ) 'እ' + any(touchVowels) > index(አ_ቤተሰብ,3)
  if( &layer = "default" ) + any(touchVowels) > index(አ_ቤተሰብ,2)
  if( &layer = "ከ-layer" ) + any(touchVowels) > index(ከ_ቤተሰብ,2) 
  if( &layer = "ኸ-layer" ) + any(touchVowels) > index(ኸ_ቤተሰብ,2)
c  if( &layer = "ኸ-extra" ) + any(touchVowels) > index(ኸ_ቤተሰብ,2)
  if( &layer = "ዐ-layer" ) + any(touchVowels) > index(ዐ_ቤተሰብ,2)
  if( &layer = "ወ-layer" ) + any(touchVowels) > index(ወ_ቤተሰብ,2)
  if( &layer = "ዘ-layer" ) + any(touchVowels) > index(ዘ_ቤተሰብ,2) 
  if( &layer = "ዠ-layer" ) + any(touchVowels) > index(ዠ_ቤተሰብ,2)
c  if( &layer = "ዠ-extra" ) + any(touchVowels) > index(ዠ_ቤተሰብ,2) 
  if( &layer = "የ-layer" ) + any(touchVowels) > index(የ_ቤተሰብ,2)
  if( &layer = "ደ-layer" ) + any(touchVowels) > index(ደ_ቤተሰብ,2)
  if( &layer = "ዸ-extra" ) + any(touchVowels) > index(ዸ_ቤተሰብ,2)
  if( &layer = "ጀ-layer" ) + any(touchVowels) > index(ጀ_ቤተሰብ,2)
  if( &layer = "ገ-layer" ) + any(touchVowels) > index(ገ_ቤተሰብ,2)
c  if( &layer = "ጘ-layer" ) + any(touchVowels) > index(ጘ_ቤተሰብ,2)
  if( &layer = "ጘ-extra" ) + any(touchVowels) > index(ጘ_ቤተሰብ,2)
  if( &layer = "ጠ-layer" ) + any(touchVowels) > index(ጠ_ቤተሰብ,2) 
  if( &layer = "ጨ-layer" ) + any(touchVowels) > index(ጨ_ቤተሰብ,2)
  if( &layer = "ጰ-layer" ) + any(touchVowels) > index(ጰ_ቤተሰብ,2) 
  if( &layer = "ጸ-layer" ) + any(touchVowels) > index(ጸ_ቤተሰብ,2)
  if( &layer = "ፀ-layer" ) + any(touchVowels) > index(ፀ_ቤተሰብ,2)
 c if( &layer = "ፀ-extra" ) + any(touchVowels) > index(ፀ_ቤተሰብ,2)
  if( &layer = "ፈ-layer" ) + any(touchVowels) > index(ፈ_ቤተሰብ,2)
  if( &layer = "ፐ-layer" ) + any(touchVowels) > index(ፐ_ቤተሰብ,2)
 c if( &layer = "ፐ-extra" ) + any(touchVowels) > index(ፐ_ቤተሰብ,2)
  if( &layer = "𞟠-extra" ) + any(touchVowels) > index(𞟠_ቤተሰብ,2)

  if( &layer = "ቈ-layer" ) + any(touchVowelsLabialvelar) > index(ቈ_ቤተሰብ,2)
  if( &layer = "ኈ-layer" ) + any(touchVowelsLabialvelar) > index(ኈ_ቤተሰብ,2)
  if( &layer = "ኰ-layer" ) + any(touchVowelsLabialvelar) > index(ኰ_ቤተሰብ,2)
  if( &layer = "ዀ-layer" ) + any(touchVowelsLabialvelar) > index(ዀ_ቤተሰብ,2)
  if( &layer = "ጐ-layer" ) + any(touchVowelsLabialvelar) > index(ጐ_ቤተሰብ,2)
  if( &layer = "ቘ-extra" ) + any(touchVowelsLabialvelar) > index(ቘ_ቤተሰብ,2)
  if( &layer = "ⶓ-extra" ) + any(touchVowelsLabialvelar) > index(ⶓ_ቤተሰብ,2)
  
c
c ---------------------End of Mobile Input ------------------------------------------------------
c
c Note: Given the new touch-specific logic above, much or all of the following that
c       address letter composition may no longer be relevant.  That is, it may all effectively
c       be $keymanonly statements.
c
c ---------------------Input of Double Strike Letters-------------------------------------------
  'ስ' + 's' > 'ሥ'
  'ህ' + 'h' > 'ኅ'
  'ጽ' + 'S' > 'ፅ'
c ---------------------End Input of Double Strike Letters---------------------------------------

c ---------------------Input of Lone Vowels-----------------------------------------------------
c 

c Alef-A Vowels as Vowels
  + any(loneVowelKeys) > index(loneVowelEthio,1)
  any(loneVowelEthio) + "'" > index(loneVowelEthio,1) deadkey(1)  

  'እ' + any(ግዕዝ-key) > 'ኧ'
  'አ' + any(ራብዕ-key) > 'ኣ'
  'ኢ' + any(ግዕዝ-key) > 'ኤ'

c Aynu-A Vowels as Regular Syllables

  'ዐ' + any(ራብዕ-key) > 'ዓ'
  'ዒ' + any(ግዕዝ-key) > 'ዔ'

c ---------------------End Input of Lone Vowels-------------------------------------------------

c ---------------------Input of Basic Syllables-------------------------------------------------
  any(ሳድስ) + any(ግዕዝ-key) > index(ግዕዝ,1)
  any(ሳድስ) + any(ካዕብ-key) > index(ካዕብ,1)
  any(ሳድስ) + any(ሣልስ-key) > index(ሣልስ,1)
  any(ሳድስ) + any(ራብዕ-key) > index(ራብዕ,1)
$keymanonly:   any(ሳድስ) + any(ኃምስ-key) > index(ኃምስ,1)
  any(ሣልስ) + any(ግዕዝ-key) > index(ኃምስ,1)  
  any(ሳድስ) + any(ሳብዕ-key) > index(ሳብዕ,1)

c Support basic labiovelars for borrowed words from other languages.
  any(ካዕብ፡መደበኛ) + any(ራብዕ-key) > index(ዘመደ፡ራብዕ፡መደበኛ,1)
c ---------------------End Input of Basic Syllables---------------------------------------------

c ---------------------Input of Punctuation-----------------------------------------------------
c 
c The only dead key for entry of special characters and
c to handle the ge'ez + lone vowel problem:  ግእዝ and not ገዝ
c 
  any(ሳድስ)      + "'" > index(ሳድስ,1) deadkey(1)
$keymanonly: any(ሳድስ) + [CTRL ' '] > index(ሳድስ,1) deadkey(1)
  any(ካዕብ፡መደበኛ) + "'" > index(ካዕብ፡መደበኛ,1) deadkey(1)
  any(ካዕብ፡ልዩ)   + "'" > index(ካዕብ፡ልዩ,1) deadkey(1)
  any(ዘመደ፡ሣልስ)  + "'" > index(ዘመደ፡ሣልስ,1) deadkey(1)             
  any(ሣልስ)      + "'" > index(ሣልስ,1) deadkey(1)


+ ':'     > '፡'
$keymanonly: + [SHIFT ' '] > '፡'
  '፡' + ':' > '።'
  '፡' + '-' > '፦'
  '፡' + '+' > '፠'
  '፡' + '#' > '፨'  
  '።' + ':' > ':'
  ':' + ':' > '፡'  c makes circular
  
any(Ethiopic) + '.' > index(Ethiopic,1) '።'
  '።' + '.' > '..'
  '.' + '.' > '..'
  '።' + any(arabNumbersWithZero) > '.' index(arabNumbersWithZero,2)
   
      + ',' > '፣'
  '፣' + ',' > '፥'
  '፥' + ',' > ','
  ',' + ',' > '፣'  c makes circular
  '፣' + any(arabNumbersWithZero) > ',' index(arabNumbersWithZero,2)


      + ';' > '፤'
  '፤' + ';' > ';'
  ';' + ';' > '፤'  c makes circular

      + '<' > '‹'
      + '>' > '›'
  '‹' + '<' > '«'
  '›' + '>' > '»'
  '«' + '<' > '<'
  '»' + '>' > '>'
c ---------------------End Input of Punctuation-------------------------------------------------

c ---------------------Input of European Characters---------------------------------------------
$keymanonly:  store(euro)    'ÇçÐðÝýÿßÑñ'
$keymanonly:  store(euroMap) 'ጭችድድይይይጽኝኝ'
$keymanonly:  + any(euro) > index(euroMap,1)

$keymanonly:  'ጽ' + 'ß' > 'ፅ'
$keymanonly:  + '¨' > U+135F

$keymanonly:  store(euroVowels) 'àÀáÁâÂãÃåÅùÙúÚûÛüÜìÌíÍîÎïÏæÆéÉèÈêÊëËòÒóÓôÔõÕöÖøØ'
c  this mapping has an Amharic bias
$keymanonly:  store(euroVowelsMap) 'አአአአአአአአአአኡኡኡኡኡኡኡኡኢኢኢኢኢኢኢኢኤኤእእእእእእእእኦኦኦኦኦኦኦኦኦኦኦኦ'
$keymanonly:  + any(euroVowels) > index(euroVowelsMap,1)
c ---------------------End Input of European Characters-----------------------------------------

c ---------------------Input of Numbers---------------------------------------------------------

c Special Rule for Arabic Numerals
c 
c The following attempts to auto-correct the use of Ethiopic wordspace and
c Ethiopic comma within an Arabic numeral context.  Ethiopic wordspace gets
c used erroneously in time formats and Ethiopic commas as an order of thousands
c delimiter. The correction context is not known until numerals appear on _both_
c sides of the punctuation.
c 
  any(arabNumbersWithZero) any(ethWordspaceOrComma) + any(arabNumbers) > index(arabNumbersWithZero,1) index(ColonOrComma,2) index(arabNumbers,3)

c Ethiopic Numerals

  '#' + '1' > '፩'
  '#' + any(ArabOnes) > index(ones,2)

c special cases for multiples of one
    '፩' + '0' >   '፲'
    '፲' + '0' >   '፻'
    '፻' + '0' >  '፲፻'
   '፲፻' + '0' >   '፼'
    '፼' + '0' >  '፲፼'    
   '፲፼' + '0' >  '፻፼' 
   '፻፼' + '0' > '፲፻፼'
  '፲፻፼' + '0' >  '፼፼'
   '፼፼' + '0' > context beep  c do not go any higher, we could beep here

c upto the order of 100 million
  any(ones)      + '0' > index(tens,1)
  any(tens)      + '0' > index(ones,1)  '፻' c Hundreds
  any(ones)  '፻' + '0' > index(tens,1)  '፻' c Thousands
  any(tens)  '፻' + '0' > index(ones,1)  '፼' c Ten Thousands
  any(ones)  '፼' + '0' > index(tens,1)  '፼' c Hundred Thousands
  any(tens)  '፼' + '0' > index(ones,1) '፻፼' c Millions
  any(ones) '፻፼' + '0' > index(tens,1) '፻፼' c Ten Millions
  any(tens) '፻፼' + '0' > index(ones,1) '፼፼' c Hundred Millions

c enhance this later, look for something that can copy a match over
  any(ethNumbers) + any(arabNumbers) > index(ethNumbers,1)  index(ethNumbers,2)

+ [T_THOUSAND] > '፲፻'
c ---------------------End Input of Numbers-----------------------------------------------------

c
c  Review if this is still needed
c

c ---------------------Extensions for Touch Layout----------------------------------------------
c
c For Amharic, extensions are Tigrinya and Xamtanga.
c These keys cannot be entered via a physical keyboard, and only in an upper layer of a touch layout.

  store(TouchExtended፡ግዕዝ)  'ቐጘ'
  store(TouchExtended፡ካዕብ)  'ቑጙ'
  store(TouchExtended፡ሣልስ)  'ቒጚ'
  store(TouchExtended፡ራብዕ)  'ቓጛ'
  store(TouchExtended፡ኃምስ)  'ቔጜ'
  store(TouchExtended፡ሳድስ)  'ቕጝ'
  store(TouchExtended፡ሳብዕ)  'ቖጞ'

  any(TouchExtended፡ሳድስ) + any(ግዕዝ-key) > index(TouchExtended፡ግዕዝ,1)
  any(TouchExtended፡ሳድስ) + any(ካዕብ-key) > index(TouchExtended፡ካዕብ,1)
  any(TouchExtended፡ሳድስ) + any(ሣልስ-key) > index(TouchExtended፡ሣልስ,1)
  any(TouchExtended፡ሳድስ) + any(ራብዕ-key) > index(TouchExtended፡ራብዕ,1)
$keymanonly:   any(TouchExtended፡ሳድስ) + any(ኃምስ-key) > index(TouchExtended፡ኃምስ,1)
  any(TouchExtended፡ሣልስ) + any(ግዕዝ-key) > index(TouchExtended፡ኃምስ,1)  
  any(TouchExtended፡ሳድስ) + any(ሳብዕ-key) > index(TouchExtended፡ሳብዕ,1)
c ---------------------End Input of Basic Syllables---------------------------------------------

c ---------------------Input of Labiovelars-----------------------------------------------------
  store(TouchExtended፡ሳድስ፡መደበኛ)     'ቕኽጝ'
  store(TouchExtended፡ካዕብ፡መደበኛ)     'ቑኹጙ'
  store(TouchExtended፡ዘመደ፡ራብዕ፡መደበኛ) 'ቛዃጟ'

c 'QKG'
  store(TouchExtended፡ካዕብ፡ልዩ)  'ቑኹጙ'
  store(TouchExtended፡ዘመደ፡ግዕዝ) 'ቘዀⶓ'
  store(TouchExtended፡ዘመደ፡ካዕብ) 'ቝዅⶖ'
  store(TouchExtended፡ዘመደ፡ሣልስ) 'ቚዂⶔ'
  store(TouchExtended፡ዘመደ፡ራብዕ) 'ቛዃጟ'
  store(TouchExtended፡ዘመደ፡ኃምስ) 'ቜዄⶕ'

  any(TouchExtended፡ካዕብ፡መደበኛ) + any(ራብዕ-key) > index(TouchExtended፡ዘመደ፡ራብዕ፡መደበኛ,1)
  any(TouchExtended፡ካዕብ፡ልዩ)   + any(ግዕዝ-key) > index(TouchExtended፡ዘመደ፡ግዕዝ,1)
  any(TouchExtended፡ካዕብ፡ልዩ)   + any(ካዕብ-key) > index(TouchExtended፡ዘመደ፡ካዕብ,1)
  any(TouchExtended፡ካዕብ፡ልዩ)   + any(ሣልስ-key) > index(TouchExtended፡ዘመደ፡ሣልስ,1)
  any(TouchExtended፡ካዕብ፡ልዩ)   + any(ራብዕ-key) > index(TouchExtended፡ዘመደ፡ራብዕ,1)
$keymanonly:   any(TouchExtended፡ካዕብ፡ልዩ)   + any(ኃምስ-key) > index(TouchExtended፡ዘመደ፡ኃምስ,1)
  any(TouchExtended፡ዘመደ፡ሣልስ)  + any(ግዕዝ-key) > index(TouchExtended፡ዘመደ፡ኃምስ,1)
c ---------------------Extensions for Touch Layout----------------------------------------------

+ [T_BEEP] > beep
+ [T_SPACER] > beep c this should be impossible, this line needed to to avoid "Warning: 2092" warnings for unknown key T_SPACER

c =====================End Functional Section===================================================
