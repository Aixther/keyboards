store(&VERSION) "10.0"
store(&KEYBOARDVERSION) '10.0'
store(&TARGETS) 'any'
c store(&ETHNOLOGUECODE) "ojb"
store(&COPYRIGHT) '(c) 2015-2024 FirstVoices, SIL International, 2015 First Peoples' U+0027 ' Cultural Foundation'
store(&NAME) 'ᐊᓂᔑᓇᐯᒧᐎᓐ'
store(&VISUALKEYBOARD) 'fv_ojibwa.kvks'

begin Unicode > use(main)

group(main) using keys


c --- START OF TOUCH LAYOUT RULES ---

c Touch layout rules originally imported from KeyMap.plist on Tue Nov 17 2015

store(&LAYOUTFILE) 'fv_ojibwa.keyman-touch-layout'

+ [SHIFT T_Q] > 'ᐁ' layer('default')
+ [SHIFT T_W] > 'ᐯ' layer('default')
+ [SHIFT T_E] > 'ᑌ' layer('default')
+ [SHIFT T_R] > 'ᑫ' layer('default')
+ [SHIFT T_T] > 'ᒉ' layer('default')
+ [SHIFT T_Y] > 'ᒣ' layer('default')
+ [SHIFT T_U] > 'ᓀ' layer('default')
+ [SHIFT T_I] > 'ᓭ' layer('default')
+ [SHIFT T_O] > 'ᔐ' layer('default')
+ [SHIFT T_P] > 'ᔦ' layer('default')
+ [SHIFT T_A] > 'ᐅ' layer('default')
+ [SHIFT T_S] > 'ᐳ' layer('default')
+ [SHIFT T_D] > 'ᑐ' layer('default')
+ [SHIFT T_F] > 'ᑯ' layer('default')
+ [SHIFT T_G] > 'ᒍ' layer('default')
+ [SHIFT T_H] > 'ᒧ' layer('default')
+ [SHIFT T_J] > 'ᓄ' layer('default')
+ [SHIFT T_K] > 'ᓱ' layer('default')
+ [SHIFT T_L] > 'ᔓ' layer('default')
+ [SHIFT T_COLON] > 'ᔪ' layer('default')
+ [SHIFT T_Z] > 'ᓬ' layer('default')
+ [SHIFT T_X] > 'ᕒ' layer('default')
+ [SHIFT T_C] > 'ᐤ' layer('default')
+ [SHIFT T_V] > 'ᐦ' layer('default')
+ [SHIFT T_B] > 'ᐞ' layer('default')
+ [SHIFT T_N] > 'ᣜ'
+ [T_Q] > 'ᐃ'
+ [T_W] > 'ᐱ'
+ [T_E] > 'ᑎ'
+ [T_R] > 'ᑭ'
+ [T_T] > 'ᒋ'
+ [T_Y] > 'ᒥ'
+ [T_U] > 'ᓂ'
+ [T_I] > 'ᓯ'
+ [T_O] > 'ᔑ'
+ [T_P] > 'ᔨ'
+ [T_A] > 'ᐊ'
+ [T_S] > 'ᐸ'
+ [T_D] > 'ᑕ'
+ [T_F] > 'ᑲ'
+ [T_G] > 'ᒐ'
+ [T_H] > 'ᒪ'
+ [T_J] > 'ᓇ'
+ [T_K] > 'ᓴ'
+ [T_L] > 'ᔕ'
+ [T_COLON] > 'ᔭ'
+ [T_Z] > 'ᑉ'
+ [T_X] > 'ᑦ'
+ [T_C] > 'ᒃ'
+ [T_V] > 'ᒡ'
+ [T_B] > 'ᒻ'
+ [T_N] > 'ᓐ'
+ [T_M] > 'ᔅ'
+ [T_COMMA] > 'ᔥ'
+ [T_PERIOD] > 'ᔾ'
+ [T_x66] > 'ᐧ'

store(NoDotE) 'ᐁᐯᑌᑫᒉᒣᓀᓭᔐᔦ'
store(LDotE) 'ᐌᐺᑗᑴᒒᒬᓉᓶᔗᔯ'
store(RDotE)'ᐍᐻᑘᑵᒓᒭᓊᓷᔘᔰ'
store(EKeys) [SHIFT T_Q] [SHIFT T_W] [SHIFT T_E] [SHIFT T_R] [SHIFT T_T] [SHIFT T_Y] [SHIFT T_U] [SHIFT T_I] [SHIFT T_O] [SHIFT T_P]
'ᐧ' + any(EKeys) > index(LDotE,2)

store(NoDotN) 'ᓂᓄᓇ'
store(LongN) 'ᓃᓅᓈ'
any(NoDotN) + any(LongKey) > index(LongN,2)
'ᐧ' + [T_J] > 'ᓋ' c right variant ᓌ
'ᓋ' + any(LongKey) > 'ᓍ' c right variant ᓎ
c [T_U] [SHIFT T_J] [T_J]

store(SNoDot) 'ᐃᐅᐊᐱᐳᐸᑎᑐᑕᑭᑯᑲᒋᒍᒐᒥᒧᒪᓯᓱᓴᔑᔓᔕᔨᔪᔭ'
store(LNoDot) 'ᐄᐆᐋᐲᐴᐹᑏᑑᑖᑮᑰᑳᒌᒎᒑᒦᒨᒫᓰᓲᓵᔒᔔᔖᔩᔫᔮ'
store(SLDot)  'ᐎᐒᐗᐼᑀᑄᑙᑝᑡᑶᑺᑾᒔᒘᒜᒮᒲᒶᓸᓼᔀᔙᔝᔡᔱᔵᔹ'
store(SRDot) 'ᐏᐓᐘᐽᑁᑅᑚᑞᑢᑷᑻᑿᒕᒙᒝᒯᒳᒷᓹᓽᔁᔚᔞᔢᔲᔶᔺ'
store(LRDot) 'ᐑᐕᐚᐿᑃᑇᑜᑠᑤᑹᑽᒁᒗᒛᒟᒱᒵᒹᓻᓿᔃᔜᔠᔤᔴᔸᔼ'
store(LLDot)  'ᐐᐔᐙᐾᑂᑆᑛᑟᑣᑸᑼᒀᒖᒚᒞᒰᒴᒸᓺᓾᔂᔛᔟᔣᔳᔷᔻ'
store(NoDotKey) [T_Q] [SHIFT T_A] [T_A] [T_W] [SHIFT T_S] [T_S] [T_E] [SHIFT T_D] [T_D] [T_R] [SHIFT T_F] [T_F] [T_T] [SHIFT T_G] [T_G] [T_Y] [SHIFT T_H] [T_H] [T_I] [SHIFT T_K] [T_K] [T_O] [SHIFT T_L] [T_L] [T_P] [SHIFT T_COLON] [T_COLON]  
store(LongKey) [T_x68]
store(DotKey) [T_x66]

any(SNoDot) + [T_x68] > index(LNoDot,1)
'ᐧ' + any(NoDotKey) > index(SLDot,2)
any(SLDot) + any(LongKey) > index(LLDot,1)
'ᐧ' + [SHIFT T_B] > 'ᣜ' c right variant ᣝ

c --- END OF TOUCH LAYOUT RULES ---


c --- START OF DESKTOP RULES ---
c syllabic stores
store(VowelKeys) [K_I] [K_O] [K_A]
store(ShortVowels) 'ᐃᐅᐊ'
store(LongVowels) 'ᐄᐆᐋ'

store(LWShortVowels) 'ᐎᐒᐗ'
store(LWLongVowels) 'ᐐᐔᐙ'
store(RWShortVowels) 'ᐏᐓᐘ'
store(RWLongVowels) 'ᐑᐕᐚ'

store(BPShortVowels) 'ᐱᐳᐸ'
store(BPLongVowels) 'ᐲᐴᐹ'
store(LBPWShortVowels) 'ᐼᑀᑄ'
store(LBPWLongVowels) 'ᐾᑂᑆ'
store(RBPWShortVowels) 'ᐽᑁᑅ'
store(RBPWLongVowels) 'ᐿᑃᑇ'

store(DTShortVowels) 'ᑎᑐᑕ'
store(DTLongVowels) 'ᑏᑑᑖ'
store(LDTWShortVowels) 'ᑙᑝᑡ'
store(LDTWLongVowels) 'ᑛᑟᑣ'
store(RDTWShortVowels) 'ᑚᑞᑢ'
store(RDTWLongVowels) 'ᑜᑠᑤ'

store(GKShortVowels) 'ᑭᑯᑲ'
store(GKLongVowels) 'ᑮᑰᑳ'
store(LGKWShortVowels) 'ᑶᑺᑾ'
store(LGKWLongVowels) 'ᑸᑼᒀ'
store(RGKSHortVowels) 'ᑷᑻᑿ'
store(RGKWLongVowels) 'ᑹᑽᒁ'

store(JCHShortVowels) 'ᒋᒍᒐ'
store(JCHLongVowels) 'ᒌᒎᒑ'
store(LJCHWShortVowels) 'ᒔᒘᒜ'
store(LJCHWLongVowels) 'ᒖᒚᒞ'
store(RLJCHWShortVowels) 'ᒕᒙᒝ'
store(RJCHWLongVowels) 'ᒗᒛᒟ'

store(MShortVowels) 'ᒥᒧᒪ'
store(MLongVowels) 'ᒦᒨᒫ'
store(LMWShortVowels) 'ᒮᒲᒶ'
store(LMWLongVowels) 'ᒰᒴᒸ'
store(RMWShortVowels) 'ᒯᒳᒷ'
store(RMWLongVowels) 'ᒱᒵᒹ'

store(NShortVowels) 'ᓂᓄᓇ'
store(NLongVowels) 'ᓃᓅᓈ'

store(SZShortVowels) 'ᓯᓱᓴ'
store(SZLongVowels) 'ᓰᓲᓵ'
store(LSZWShortVowels) 'ᓸᓼᔀ'
store(LSZWLongVowels) 'ᓺᓾᔂ'
store(RSZWShortVowels) 'ᓹᓽᔁ'
store(RSZWLongVowels) 'ᓻᓿᔃ'

store(SHZHShortVowels) 'ᔑᔓᔕ'
store(SHZHLongVowels) 'ᔒᔔᔖ'
store(LSHZHWShortVowels) 'ᔙᔝᔡ'
store(LSHZHWLongVowels) 'ᔛᔟᔣ'
store(RSHZHWShortVowels) 'ᔚᔞᔢ'
store(RSHZHWLongVowels) 'ᔜᔠ ᔤ'

store(YShortVowels) 'ᔨᔪᔭ'
store(YLongVowels) 'ᔩᔫᔮ'
store(LYWShortVowels) 'ᔱᔵᔹ'
store(LYWLongVowels) 'ᔳᔷᔻ'
store(RYWShortVowels) 'ᔲᔶᔺ'
store(RYWLongVowels) 'ᔴᔸᔼ'


c Standalone Vowels

+ 'e' > 'ᐁ'
+ 'i' > 'ᐃ'
'ᐃ' + [K_I] > 'ᐄ'
+ 'o' > 'ᐅ'
'ᐅ' + [K_O] > 'ᐆ'
+ 'a' > 'ᐊ'
'ᐊ' + [K_A] > 'ᐋ'
'ᐁ' + [K_W] > 'ᐌ'
any(ShortVowels) + [K_W] > index(LWShortVowels,1)
any(LongVowels) + [K_W] > index(LWLongVowels,1)

c Standalone Consonants

+ 'h' > 'ᐦ'
+ [SHIFT K_L] > 'ᓬ'
+ [SHIFT K_R] > 'ᕒ'


c Syllable Building
+ [K_B] > 'ᑉ'
+ [K_P] > 'ᑉ'
'ᑉ' + [K_E] > 'ᐯ'
'ᑉ' + any(VowelKeys) > index(BPShortVowels,2)
any(BPShortVowels) + any(VowelKeys) > index(BPLongVowels,1)
any(BPLongVowels) + [K_BKSP] > index(BPShortVowels,1)
'ᑉ' + [K_W] > 'ᐺ'
'ᐺ' + [K_E] > 'ᐺ' c R dot ᐻ
'ᐺ' + any(VowelKeys) > index(LBPWShortVowels,2)
any(LBPWShortVowels) + any(VowelKeys) > index(LBPWLongVowels,1)
any(LBPWLongVowels) + [K_BKSP] > index(LBPWShortVowels,1)

+ [K_D] > 'ᑦ'
+ [K_T] > 'ᑦ'
'ᑦ' + [K_E] > 'ᑌ'
'ᑦ' + any(VowelKeys) > index(DTShortVowels,2)
any(DTShortVowels) + any(VowelKeys) > index(DTLongVowels,1)
any(DTLongVowels) + [K_BKSP] > index(DTShortVowels,1)
'ᑦ' + [K_W] > 'ᑗ' c right variant ᑘ
'ᑗ' + [K_E] > 'ᑗ'
'ᑗ' + any(VowelKeys) > index(LDTWShortVowels,2)
any(LDTWShortVowels) + any(VowelKeys) > index(LDTWLongVowels,1)
any(LDTWLongVowels) + [K_BKSP] > index(LDTWShortVowels,1)

+ [K_G] > 'ᒃ'
+ [K_K] > 'ᒃ'
'ᒃ' + [K_E] > 'ᑫ'
'ᒃ' + any(VowelKeys) > index(GKShortVowels,2)
any(GKShortVowels) + any(VowelKeys) > index(GKLongVowels,1)
any(GKLongVowels) + [K_BKSP] > index(GKShortVowels,1)
'ᒃ' + [K_W] > 'ᑴ' c right variant ᑵ
'ᑴ' + [K_E] > 'ᑴ'
'ᑴ' + any(VowelKeys) > index(LGKWShortVowels,2)
any(LGKWShortVowels) + any(VowelKeys) > index(LGKWLongVowels,1)
any(LGKWLongVowels) + [K_BKSP] > index(LGKWShortVowels,1)

+ [K_J] > 'ᒡ'
'c' + [K_H] > 'ᒡ'
'ᒡ' + [K_E] > 'ᒉ'
'ᒡ' + any(VowelKeys) > index(JCHShortVowels,2)
any(JCHShortVowels) + any(VowelKeys) > index(JCHLongVowels,1)
any(JCHLongVowels) + [K_BKSP] > index(JCHShortVowels,1)
'ᒡ' + [K_W] > 'ᒒ' c right variant ᒓ
'ᒒ' + [K_E] > 'ᒒ'
'ᒒ' + any(VowelKeys) > index(LJCHWShortVowels,2)
any(LJCHWShortVowels) + any(VowelKeys) > index(LJCHWLongVowels,1)
any(LJCHWLongVowels) + [K_BKSP] > index(LJCHWShortVowels,1)

+ [K_M] > 'ᒻ'
'ᒻ' + [K_E] > 'ᒣ'
'ᒻ' + any(VowelKeys) > index(MShortVowels,2)
any(MShortVowels) + any(VowelKeys) > index(MLongVowels,1)
any(MLongVowels) + [K_BKSP] > index(MShortVowels,1)
'ᒻ' + [K_W] > 'ᒬ' c right variant ᒭ
'ᒬ' + [K_E] > 'ᒬ'
'ᒬ' + any(VowelKeys) > index(LMWShortVowels,2)
any(LMWShortVowels) + any(VowelKeys) > index(LMWLongVowels,1)
any(LMWLongVowels) + [K_BKSP] > index(LMWShortVowels,1)

+ [K_N] > 'ᓐ'
'ᓐ' + [K_E] > 'ᓀ'
'ᓐ' + any(VowelKeys) > index(NShortVowels,2)
any(NShortVowels) + any(VowelKeys) > index(NLongVowels,1)
any(NLongVowels) + [K_BKSP] > index(NShortVowels,1)
'ᓐ' + [K_W] > 'ᓉ' c right variant ᓊ
'ᓉ' + [K_E] > 'ᓉ'
'ᓉ' + [K_A] > 'ᓋ' c right variant ᓌ
'ᓋ' + [K_A] > 'ᓍ' c right variant ᓎ
'ᓎ' + [K_BKSP] > 'ᓋ'

+ [K_L] > 'ᓫ'
'ᓫ' + [K_E] > 'ᓀᓫ'
'ᓫ' + any(VowelKeys) > index(NShortVowels,2)'ᓫ'
'ᓂᓫ' + [K_I] > 'ᓃᓫ'
'ᓄᓫ' + [K_O] > 'ᓅᓫ'
'ᓇᓫ' + [K_A] > 'ᓈᓫ'
'ᓃᓫ' + [K_BKSP] > 'ᓂᓫ'
'ᓅᓫ' + [K_BKSP] > 'ᓄᓫ'
'ᓈᓫ' + [K_BKSP] > 'ᓇᓫ'

+ [K_R] > 'ᕑ'
'ᕑ' + [K_E] > 'ᓀᕑ'
'ᕑ' + any(VowelKeys) > index(NShortVowels,2)'ᕑ'
'ᓂᕑ' + [K_I] > 'ᓃᕑ'
'ᓄᕑ' + [K_O] > 'ᓅᕑ'
'ᓇᕑ' + [K_A] > 'ᓈᕑ'
'ᓃᕑ' + [K_BKSP] > 'ᓂᕑ'
'ᓅᕑ' + [K_BKSP] > 'ᓄᕑ'
'ᓈᕑ' + [K_BKSP] > 'ᓇᕑ'

+ [K_S] > 'ᔅ'
+ [K_Z] > 'ᔅ'
'ᔅ' + [K_E] > 'ᓭ'
'ᔅ' + any(VowelKeys) > index(SZShortVowels,2)
any(SZShortVowels) + any(VowelKeys) > index(SZLongVowels,1)
any(SZLongVowels) + [K_BKSP] > index(SZShortVowels,1)
'ᔅ' + [K_W] > 'ᓶ' c right variant ᓷ
'ᓶ' + [K_E] > 'ᓶ'
'ᓶ' + any(VowelKeys) > index(LSZWShortVowels,2)
any(LSZWShortVowels) + any(VowelKeys) > index(LSZWLongVowels,1)
any(LSZWLongVowels) + [K_BKSP] > index(LSZWShortVowels,1)

'ᔅ' + [K_H] > 'ᔥ'
'ᔥ' + [K_E] > 'ᔐ'
'ᔥ' + any(VowelKeys) > index(SHZHShortVowels,2)
any(SHZHShortVowels) + any(VowelKeys) > index(SHZHLongVowels,1)
any(SHZHLongVowels) + [K_BKSP] > index(SHZHShortVowels,1)
'ᔥ' + [K_W] > 'ᔗ' c ᔘ right variant
'ᔗ' + [K_E] > 'ᔗ'
'ᔗ' + any(VowelKeys) > index(LSHZHWShortVowels,2)
any(LSHZHWShortVowels) + any(VowelKeys) > index(LSHZHWLongVowels,1)
any(LSHZHWLongVowels) + [K_BKSP] > index(LSHZHWShortVowels,1)

+ [K_Y] > 'ᔾ'
'ᔾ' + [K_E] > 'ᔦ'
'ᔾ' + any(VowelKeys) > index(YShortVowels,2)
any(YShortVowels) + any(VowelKeys) > index(YLongVowels,1)
any(YLongVowels) + [K_BKSP] > index(YShortVowels,1)
'ᔾ' + [K_W] > 'ᔯ' c right variant ᔰ
'ᔯ' + [K_E] > 'ᔯ'
'ᔯ' + any(VowelKeys) > index(LYWShortVowels,2)
any(LYWShortVowels) + any(VowelKeys) > index(LYWLongVowels,1)
any(LYWLongVowels) + [K_BKSP] > index(LYWShortVowels,1)

+ [K_W] > 'ᐤ'
+ [SHIFT K_W] > 'ᣜ' c right variant ᣝ
'ᐤ' + [K_E] > 'ᐌ' c right variant ᐍ
'ᐤ' + any(VowelKeys) > index(LWShortVowels,2)
any(LWShortVowels) + any(VowelKeys) > index(LWLongVowels,1)
any(LWLongVowels) + [K_BKSP] > index(LWShortVowels,1)

c punctuation
+ '.' > '᙮'
+ [RALT K_PERIOD] > '.'
+ [K_LBRKT] > deadkey(Deadkey) 
+ [RALT K_LBRKT] > '['

