store(&VERSION) "10.0"
store(&KEYBOARDVERSION) '10.0'
c store(&ETHNOLOGUECODE) "ojs"
store(&COPYRIGHT) '(c) 2015-2024 FirstVoices, SIL International, 2015 First Peoples' U+0027 ' Cultural Foundation'
store(&NAME) 'ᐊᓂᔑᓂᓂᒧᐎᐣ'
store(&TARGETS) 'any'
store(&VISUALKEYBOARD) 'fv_severn_ojibwa.kvks'

begin Unicode > use(main)

group(main) using keys


c --- START OF TOUCH LAYOUT RULES ---

c Touch layout rules originally imported from KeyMap.plist on Tue Nov 17 2015

store(&LAYOUTFILE) 'fv_severn_ojibwa.keyman-touch-layout'

+ [SHIFT T_Q] > 'ᐁ' layer('default')
+ [SHIFT T_W] > 'ᐯ' layer('default')
+ [SHIFT T_E] > 'ᑌ' layer('default')
+ [SHIFT T_R] > 'ᑫ' layer('default')
+ [SHIFT T_T] > 'ᒉ' layer('default')
+ [SHIFT T_Y] > 'ᒣ' layer('default')
+ [SHIFT T_U] > 'ᓀ' layer('default')
+ [SHIFT T_I] > 'ᓭ' layer('default')
+ [SHIFT T_O] > 'ᔐ' layer('default')
+ [SHIFT T_P] > 'ᔦ' layer('default')
+ [SHIFT T_A] > 'ᐅ' layer('default')
+ [SHIFT T_S] > 'ᐳ' layer('default')
+ [SHIFT T_D] > 'ᑐ' layer('default')
+ [SHIFT T_F] > 'ᑯ' layer('default')
+ [SHIFT T_G] > 'ᒍ' layer('default')
+ [SHIFT T_H] > 'ᒧ' layer('default')
+ [SHIFT T_J] > 'ᓄ' layer('default')
+ [SHIFT T_K] > 'ᓱ' layer('default')
+ [SHIFT T_L] > 'ᔓ' layer('default')
+ [SHIFT T_COLON] > 'ᔪ' layer('default')
+ [SHIFT T_Z] > 'ᓬ' layer('default')
+ [SHIFT T_X] > 'ᕒ' layer('default')
+ [SHIFT T_C] > 'ᐤ' layer('default')
+ [SHIFT T_V] > 'ᐦ' layer('default')
+ [SHIFT T_B] > 'ᐞ' layer('default')
+ [SHIFT T_N] > 'ᣜ' layer('default')
+ [T_Q] > 'ᐃ'
+ [T_W] > 'ᐱ'
+ [T_E] > 'ᑎ'
+ [T_R] > 'ᑭ'
+ [T_T] > 'ᒋ'
+ [T_Y] > 'ᒥ'
+ [T_U] > 'ᓂ'
+ [T_I] > 'ᓯ'
+ [T_O] > 'ᔑ'
+ [T_P] > 'ᔨ'
+ [T_A] > 'ᐊ'
+ [T_S] > 'ᐸ'
+ [T_D] > 'ᑕ'
+ [T_F] > 'ᑲ'
+ [T_G] > 'ᒐ'
+ [T_H] > 'ᒪ'
+ [T_J] > 'ᓇ'
+ [T_K] > 'ᓴ'
+ [T_L] > 'ᔕ'
+ [T_COLON] > 'ᔭ'
+ [T_Z] > 'ᑊ'
+ [T_X] > 'ᐟ'
+ [T_C] > 'ᐠ'
+ [T_V] > 'ᐨ'
+ [T_B] > 'ᒼ'
+ [T_N] > 'ᐣ'
+ [T_M] > 'ᐢ'
+ [T_COMMA] > 'ᐡ'
+ [T_PERIOD] > 'ᣞ'
+ [T_x66] > 'ᐧ'

store(NoDotE) 'ᐁᐯᑌᑫᒉᒣᓀᓭᔐᔦ'
store(LDotE) 'ᐌᐺᑗᑴᒒᒬᓉᓶᔗᔯ'
store(RDotE)'ᐍᐻᑘᑵᒓᒭᓊᓷᔘᔰ'
store(EKeys) [SHIFT T_Q] [SHIFT T_W] [SHIFT T_E] [SHIFT T_R] [SHIFT T_T] [SHIFT T_Y] [SHIFT T_U] [SHIFT T_I] [SHIFT T_O] [SHIFT T_P]
'ᐧ' + any(EKeys) > index(LDotE,2)

store(NoDotN) 'ᓂᓄᓇ'
store(LongN) 'ᓃᓅᓈ'
any(NoDotN) + any(LongKey) > index(LongN,2)
'ᐧ' + [T_J] > 'ᓋ' c right variant ᓌ
'ᓋ' + any(LongKey) > 'ᓍ' c right variant ᓎ

'ᓀ' + [T_x66] > 'ᓊ'
'ᓇ' + [T_x66] > 'ᓌ'
'ᓌ' + any(LongKey) > 'ᓎ' c left variants ᓉ ᓋ ᓍ
c [T_U] [SHIFT T_J] [T_J]

store(SNoDot) 'ᐃᐅᐊᐱᐳᐸᑎᑐᑕᑭᑯᑲᒋᒍᒐᒥᒧᒪᓯᓱᓴᔑᔓᔕᔨᔪᔭ'
store(LNoDot) 'ᐄᐆᐋᐲᐴᐹᑏᑑᑖᑮᑰᑳᒌᒎᒑᒦᒨᒫᓰᓲᓵᔒᔔᔖᔩᔫᔮ'
store(SLDot)  'ᐎᐒᐗᐼᑀᑄᑙᑝᑡᑶᑺᑾᒔᒘᒜᒮᒲᒶᓸᓼᔀᔙᔝᔡᔱᔵᔹ'
store(SRDot) 'ᐏᐓᐘᐽᑁᑅᑚᑞᑢᑷᑻᑿᒕᒙᒝᒯᒳᒷᓹᓽᔁᔚᔞᔢᔲᔶᔺ'
store(LRDot) 'ᐑᐕᐚᐿᑃᑇᑜᑠᑤᑹᑽᒁᒗᒛᒟᒱᒵᒹᓻᓿᔃᔜᔠᔤᔴᔸᔼ'
store(LLDot)  'ᐐᐔᐙᐾᑂᑆᑛᑟᑣᑸᑼᒀᒖᒚᒞᒰᒴᒸᓺᓾᔂᔛᔟᔣᔳᔷᔻ'
store(NoDotKey) [T_Q] [SHIFT T_A] [T_A] [T_W] [SHIFT T_S] [T_S] [T_E] [SHIFT T_D] [T_D] [T_R] [SHIFT T_F] [T_F] [T_T] [SHIFT T_G] [T_G] [T_Y] [SHIFT T_H] [T_H] [T_I] [SHIFT T_K] [T_K] [T_O] [SHIFT T_L] [T_L] [T_P] [SHIFT T_COLON] [T_COLON]  
store(LongKey) [T_x68]
store(DotKey) [T_x66]

any(SNoDot) + [T_x68] > index(LNoDot,1)
'ᐧ' + any(NoDotKey) > index(SLDot,2)
any(SLDot) + any(LongKey) > index(LLDot,1)
'ᐧ' + [SHIFT T_B] > 'ᣜ' c right variant ᣝ

c --- END OF TOUCH LAYOUT RULES ---


c --- START OF DESKTOP RULES ---

c syllabic stores
store(VowelKeys) [K_I] [K_O] [K_A]
store(ShortVowels) 'ᐃᐅᐊ'
store(LongVowels) 'ᐄᐆᐋ'

store(WShortVowels) 'ᐎᐒᐗ'
store(WLongVowels) 'ᐐᐔᐙ'

store(BPShortVowels) 'ᐱᐳᐸ'
store(BPLongVowels) 'ᐲᐴᐹ'
store(BPWShortVowels) 'ᐼᑀᑄ'
store(BPWLongVowels) 'ᐾᑂᑆ'

store(DTShortVowels) 'ᑎᑐᑕ'
store(DTLongVowels) 'ᑏᑑᑖ'
store(DTWShortVowels) 'ᑙᑝᑡ'
store(DTWLongVowels) 'ᑛᑟᑣ'

store(GKShortVowels) 'ᑭᑯᑲ'
store(GKLongVowels) 'ᑮᑰᑳ'
store(GKWShortVowels) 'ᑶᑺᑾ'
store(GKWLongVowels) 'ᑸᑼᒀ'

store(JCHShortVowels) 'ᒋᒍᒐ'
store(JCHLongVowels) 'ᒌᒎᒑ'
store(JCHWShortVowels) 'ᒔᒘᒜ'
store(JCHWLongVowels) 'ᒖᒚᒞ'

store(MShortVowels) 'ᒥᒧᒪ'
store(MLongVowels) 'ᒦᒨᒫ'
store(MWShortVowels) 'ᒮᒲᒶ'
store(MWLongVowels) 'ᒰᒴᒸ'

store(NShortVowels) 'ᓂᓄᓇ'
store(NLongVowels) 'ᓃᓅᓈ'

store(SZShortVowels) 'ᓯᓱᓴ'
store(SZLongVowels) 'ᓰᓲᓵ'
store(SZWShortVowels) 'ᓸᓼᔀ'
store(SZWLongVowels) 'ᓺᓾᔂ'

store(SHZHShortVowels) 'ᔑᔓᔕ'
store(SHZHLongVowels) 'ᔒᔔᔖ'
store(SHZHWShortVowels) 'ᔙᔝᔡ'
store(SHZHWLongVowels) 'ᔛᔟᔣ'

store(YShortVowels) 'ᔨᔪᔭ'
store(YLongVowels) 'ᔩᔫᔮ'
store(YWShortVowels) 'ᔱᔵᔹ'
store(YWLongVowels) 'ᔳᔷᔻ'

c Standalone Vowels

+ 'e' > 'ᐁ'
+ 'i' > 'ᐃ'
'ᐃ' + [K_I] > 'ᐄ'
+ 'o' > 'ᐅ'
'ᐅ' + [K_O] > 'ᐆ'
+ 'a' > 'ᐊ'
'ᐊ' + [K_A] > 'ᐋ'
'ᐁ' + [K_W] > 'ᐌ'
any(ShortVowels) + [K_W] > index(WShortVowels,1)
any(LongVowels) + [K_W] > index(WLongVowels,1)

c Standalone Consonants

+ 'h' > 'ᐦ'
+ [K_L] > 'ᓬ'
+ [SHIFT K_L] > 'ᓫ'
+ [K_R] > 'ᕒ'
+ [SHIFT K_R] > 'ᕑ'

c Syllable Building

+ [K_B] > 'ᑊ'
+ [K_P] > 'ᑊ'
'ᑊ' + [K_E] > 'ᐯ'
'ᑊ' + any(VowelKeys) > index(BPShortVowels,2)
any(BPShortVowels) + any(VowelKeys) > index(BPLongVowels,1)
any(BPLongVowels) + [K_BKSP] > index(BPShortVowels,1)
'ᑊ' + [K_W] > 'ᐺ'
'ᐺ' + [K_E] > 'ᐺ' 
'ᐺ' + any(VowelKeys) > index(BPWShortVowels,2)
any(BPWShortVowels) + any(VowelKeys) > index(BPWLongVowels,1)
any(BPWLongVowels) + [K_BKSP] > index(BPWShortVowels,1)

+ [K_D] > 'ᐟ'
+ [K_T] > 'ᐟ'
'ᐟ' + [K_E] > 'ᑌ'
'ᐟ' + any(VowelKeys) > index(DTShortVowels,2)
any(DTShortVowels) + any(VowelKeys) > index(DTLongVowels,1)
any(DTLongVowels) + [K_BKSP] > index(DTShortVowels,1)
'ᐟ' + [K_W] > 'ᑗ'
'ᑗ' + [K_E] > 'ᑗ'
'ᑗ' + any(VowelKeys) > index(DTWShortVowels,2)
any(DTWShortVowels) + any(VowelKeys) > index(DTWLongVowels,1)
any(DTWLongVowels) + [K_BKSP] > index(DTWShortVowels,1)

+ [K_G] > 'ᐠ'
+ [K_K] > 'ᐠ'
'ᐠ' + [K_E] > 'ᑫ'
'ᐠ' + any(VowelKeys) > index(GKShortVowels,2)
any(GKShortVowels) + any(VowelKeys) > index(GKLongVowels,1)
any(GKLongVowels) + [K_BKSP] > index(GKShortVowels,1)
'ᐠ' + [K_W] > 'ᑴ'
'ᑴ' + [K_E] > 'ᑴ'
'ᑴ' + any(VowelKeys) > index(GKWShortVowels,2)
any(GKWShortVowels) + any(VowelKeys) > index(GKWLongVowels,1)
any(GKWLongVowels) + [K_BKSP] > index(GKWShortVowels,1)

+ [K_J] > 'ᐨ'
'c' + [K_H] > 'ᐨ'
'ᐨ' + [K_E] > 'ᒉ'
'ᐨ' + any(VowelKeys) > index(JCHShortVowels,2)
any(JCHShortVowels) + any(VowelKeys) > index(JCHLongVowels,1)
any(JCHLongVowels) + [K_BKSP] > index(JCHShortVowels,1)
'ᐨ' + [K_W] > 'ᒒ'
'ᒒ' + [K_E] > 'ᒒ'
'ᒒ' + any(VowelKeys) > index(JCHWShortVowels,2)
any(JCHWShortVowels) + any(VowelKeys) > index(JCHWLongVowels,1)
any(JCHWLongVowels) + [K_BKSP] > index(JCHWShortVowels,1)

+ [K_M] > 'ᒼ'
'ᒼ' + [K_E] > 'ᒣ'
'ᒼ' + any(VowelKeys) > index(MShortVowels,2)
any(MShortVowels) + any(VowelKeys) > index(MLongVowels,1)
any(MLongVowels) + [K_BKSP] > index(MShortVowels,1)
'ᒼ' + [K_W] > 'ᒬ' 
'ᒬ' + [K_E] > 'ᒬ'
'ᒬ' + any(VowelKeys) > index(MWShortVowels,2)
any(MWShortVowels) + any(VowelKeys) > index(MWLongVowels,1)
any(MWLongVowels) + [K_BKSP] > index(MWShortVowels,1)

+ [K_N] > 'ᐣ'
'ᐣ' + [K_E] > 'ᓀ'
'ᐣ' + any(VowelKeys) > index(NShortVowels,2)
any(NShortVowels) + any(VowelKeys) > index(NLongVowels,1)
any(NLongVowels) + [K_BKSP] > index(NShortVowels,1)
'ᐣ' + [K_W] > 'ᓉ' 
'ᓉ' + [K_E] > 'ᓉ'
'ᓉ' + [K_A] > 'ᓋ'
'ᓋ' + [K_A] > 'ᓍ'
'ᓎ' + [K_BKSP] > 'ᓋ'

+ [K_S] > 'ᐢ'
+ [K_Z] > 'ᐢ'
'ᐢ' + [K_E] > 'ᓭ'
'ᐢ' + any(VowelKeys) > index(SZShortVowels,2)
any(SZShortVowels) + any(VowelKeys) > index(SZLongVowels,1)
any(SZLongVowels) + [K_BKSP] > index(SZShortVowels,1)
'ᐢ' + [K_W] > 'ᓶ'
'ᓶ' + [K_E] > 'ᓶ'
'ᓶ' + any(VowelKeys) > index(SZWShortVowels,2)
any(SZWShortVowels) + any(VowelKeys) > index(SZWLongVowels,1)
any(SZWLongVowels) + [K_BKSP] > index(SZWShortVowels,1)

'ᐢ' + [K_H] > 'ᐡ'
'ᐡ' + [K_E] > 'ᔐ'
'ᐡ' + any(VowelKeys) > index(SHZHShortVowels,2)
any(SHZHShortVowels) + any(VowelKeys) > index(SHZHLongVowels,1)
any(SHZHLongVowels) + [K_BKSP] > index(SHZHShortVowels,1)
'ᐡ' + [K_W] > 'ᔗ'
'ᔗ' + [K_E] > 'ᔗ'
'ᔗ' + any(VowelKeys) > index(SHZHWShortVowels,2)
any(SHZHWShortVowels) + any(VowelKeys) > index(SHZHWLongVowels,1)
any(SHZHWLongVowels) + [K_BKSP] > index(SHZHWShortVowels,1)

+ [K_Y] > 'ᣞ'
'ᣞ' + [K_E] > 'ᔦ'
'ᣞ' + any(VowelKeys) > index(YShortVowels,2)
any(YShortVowels) + any(VowelKeys) > index(YLongVowels,1)
any(YLongVowels) + [K_BKSP] > index(YShortVowels,1)
'ᣞ' + [K_W] > 'ᔯ'
'ᔯ' + [K_E] > 'ᔯ'
'ᔯ' + any(VowelKeys) > index(YWShortVowels,2)
any(YWShortVowels) + any(VowelKeys) > index(YWLongVowels,1)
any(YWLongVowels) + [K_BKSP] > index(YWShortVowels,1)

+ [K_W] > 'ᐤ'
+ [SHIFT K_W] > 'ᣜ'
'ᐤ' + [K_E] > 'ᐌ'
'ᐤ' + any(VowelKeys) > index(WShortVowels,2)
any(WShortVowels) + any(VowelKeys) > index(WLongVowels,1)
any(WLongVowels) + [K_BKSP] > index(WShortVowels,1)

c punctuation
+ '.' > '᙮'
+ [RALT K_PERIOD] > '.'
+ [K_LBRKT] > deadkey(Deadkey) 
+ [RALT K_LBRKT] > '['

store(nul) 'qudfxvQETYUIOPASDFGHJKZXCVBNM'
+ any(nul) > nul
