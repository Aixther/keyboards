c This keyboard is for the a-final, right w-dot variant of Ojibwa syllabics.
store(&VERSION) '10.0'
store(&NAME) 'ᐊᓂᔑᓇᐯᒧᐏᓐ'
store(&COPYRIGHT) '© 2024 FirstVoices, First Peoples' U+0027 ' Cultural Council'
store(&KEYBOARDVERSION) '1.0'
store(&TARGETS) 'any'
store(&BITMAP) 'fv_ojibwa_rightdot.ico'
store(&VISUALKEYBOARD) 'fv_ojibwa_rightdot.kvks'
store(&LAYOUTFILE) 'fv_ojibwa_rightdot.keyman-touch-layout'

begin Unicode > use(main)

group(main) using keys

c --- START OF TOUCH LAYOUT RULES ---

c Touch layout rules originally imported from KeyMap.plist on Tue Nov 17 2015

+ [SHIFT T_Q] > 'ᐁ' layer('default')
+ [SHIFT T_W] > 'ᐯ' layer('default')
+ [SHIFT T_E] > 'ᑌ' layer('default')
+ [SHIFT T_R] > 'ᑫ' layer('default')
+ [SHIFT T_T] > 'ᒉ' layer('default')
+ [SHIFT T_Y] > 'ᒣ' layer('default')
+ [SHIFT T_U] > 'ᓀ' layer('default')
+ [SHIFT T_I] > 'ᓭ' layer('default')
+ [SHIFT T_O] > 'ᔐ' layer('default')
+ [SHIFT T_P] > 'ᔦ' layer('default')
+ [SHIFT T_A] > 'ᐅ' layer('default')
+ [SHIFT T_S] > 'ᐳ' layer('default')
+ [SHIFT T_D] > 'ᑐ' layer('default')
+ [SHIFT T_F] > 'ᑯ' layer('default')
+ [SHIFT T_G] > 'ᒍ' layer('default')
+ [SHIFT T_H] > 'ᒧ' layer('default')
+ [SHIFT T_J] > 'ᓄ' layer('default')
+ [SHIFT T_K] > 'ᓱ' layer('default')
+ [SHIFT T_L] > 'ᔓ' layer('default')
+ [SHIFT T_COLON] > 'ᔪ' layer('default')
+ [SHIFT T_Z] > 'ᓬ' layer('default')
+ [SHIFT T_X] > 'ᕒ' layer('default')
+ [SHIFT T_C] > 'ᐤ' layer('default')
+ [SHIFT T_V] > 'ᐦ' layer('default')
+ [SHIFT T_B] > 'ᐞ' layer('default')
+ [T_Q] > 'ᐃ'
+ [T_W] > 'ᐱ'
+ [T_E] > 'ᑎ'
+ [T_R] > 'ᑭ'
+ [T_T] > 'ᒋ'
+ [T_Y] > 'ᒥ'
+ [T_U] > 'ᓂ'
+ [T_I] > 'ᓯ'
+ [T_O] > 'ᔑ'
+ [T_P] > 'ᔨ'
+ [T_A] > 'ᐊ'
+ [T_S] > 'ᐸ'
+ [T_D] > 'ᑕ'
+ [T_F] > 'ᑲ'
+ [T_G] > 'ᒐ'
+ [T_H] > 'ᒪ'
+ [T_J] > 'ᓇ'
+ [T_K] > 'ᓴ'
+ [T_L] > 'ᔕ'
+ [T_COLON] > 'ᔭ'
+ [T_Z] > 'ᑉ'
+ [T_X] > 'ᑦ'
+ [T_C] > 'ᒃ'
+ [T_V] > 'ᒡ'
+ [T_B] > 'ᒻ'
+ [T_N] > 'ᓐ'
+ [T_M] > 'ᔅ'
+ [T_COMMA] > 'ᔥ'
+ [T_PERIOD] > 'ᔾ'
+ [T_x66] > 'ᐧ'

store(NoDotE) 'ᐁᐯᑌᑫᒉᒣᓀᓭᔐᔦ'
store(DotE)'ᐍᐻᑘᑵᒓᒭᓊᓷᔘᔰ'
store(EKeys) [SHIFT T_Q] [SHIFT T_W] [SHIFT T_E] [SHIFT T_R] [SHIFT T_T] [SHIFT T_Y] [SHIFT T_U] [SHIFT T_I] [SHIFT T_O] [SHIFT T_P]
'ᐧ' + any(EKeys) > index(DotE,2)

store(NoDotN) 'ᓂᓄᓇ'
store(LongN) 'ᓃᓅᓈ'
any(NoDotN) + any(LongKey) > index(LongN,2)
'ᐧ' + [T_J] > 'ᓌ'
'ᓌ' + any(LongKey) > 'ᓎ'

c short and long refers to the vowel length of the syllables in the stores
store(ShortNoDot) 'ᐃᐅᐊᐱᐳᐸᑎᑐᑕᑭᑯᑲᒋᒍᒐᒥᒧᒪᓯᓱᓴᔑᔓᔕᔨᔪᔭ'
store(LongNoDot) 'ᐄᐆᐋᐲᐴᐹᑏᑑᑖᑮᑰᑳᒌᒎᒑᒦᒨᒫᓰᓲᓵᔒᔔᔖᔩᔫᔮ'
store(ShortDot) 'ᐏᐓᐘᐽᑁᑅᑚᑞᑢᑷᑻᑿᒕᒙᒝᒯᒳᒷᓹᓽᔁᔚᔞᔢᔲᔶᔺ'
store(LongDot) 'ᐑᐕᐚᐿᑃᑇᑜᑠᑤᑹᑽᒁᒗᒛᒟᒱᒵᒹᓻᓿᔃᔜᔠᔤᔴᔸᔼ'
store(NoDotKey) [T_Q] [SHIFT T_A] [T_A] [T_W] [SHIFT T_S] [T_S] [T_E] [SHIFT T_D] [T_D] [T_R] [SHIFT T_F] [T_F] [T_T] [SHIFT T_G] [T_G] [T_Y] [SHIFT T_H] [T_H] [T_I] [SHIFT T_K] [T_K] [T_O] [SHIFT T_L] [T_L] [T_P] [SHIFT T_COLON] [T_COLON]  
store(LongKey) [T_x68]
store(DotKey) [T_x66]

any(ShortNoDot) + [T_x68] > index(LongNoDot,1)
'ᐧ' + any(NoDotKey) > index(ShortDot,2)
any(ShortDot) + any(LongKey) > index(LongDot,1)
'ᐧ' + [SHIFT T_B] > 'ᣝ'

c --- END OF TOUCH LAYOUT RULES ---


c --- START OF DESKTOP RULES ---
c syllabic stores
store(VowelKeys) [K_I] [K_O] [K_A]
store(ShortVowels) 'ᐃᐅᐊ' c i o a
store(LongVowels) 'ᐄᐆᐋ' c ii oo aa

store(WShortVowels) 'ᐏᐓᐘ' c wi wo wa
store(WLongVowels) 'ᐑᐕᐚ' c wii woo waa

store(BPShortVowels) 'ᐱᐳᐸ' c bi bo ba / pi po pa
store(BPLongVowels) 'ᐲᐴᐹ' c bii boo baa / pii poo paa
store(BPWShortVowels) 'ᐽᑁᑅ' c bwi bwo bwa / pwi pwo pwa
store(BPWLongVowels) 'ᐿᑃᑇ' c bwii bwoo bwaa / pwii pwoo pwaa

store(DTShortVowels) 'ᑎᑐᑕ' c di do da / ti to ta
store(DTLongVowels) 'ᑏᑑᑖ' c dii doo daa / tii too taa
store(DTWShortVowels) 'ᑚᑞᑢ' c dwi dwo dwa / twi two twa
store(DTWLongVowels) 'ᑜᑠᑤ' c dwii dwoo dwaa / twii twoo twaa

store(GKShortVowels) 'ᑭᑯᑲ' c gi go ga / ki ko ka
store(GKLongVowels) 'ᑮᑰᑳ' c gii goo gaa / kii koo kaa
store(GKWSHortVowels) 'ᑷᑻᑿ' c gwi gwo gwa / kwi kwo kwaa
store(GKWLongVowels) 'ᑹᑽᒁ' c gwii gwoo gwaa / kwii kwoo kwaa

store(JCHShortVowels) 'ᒋᒍᒐ' c ji jo ja / chi cho cha
store(JCHLongVowels) 'ᒌᒎᒑ' c jii joo jaa / chii choo chaa
store(JCHWShortVowels) 'ᒕᒙᒝ' c jwi jwo jwa / chwi chwo chwa
store(JCHWLongVowels) 'ᒗᒛᒟ' c jwii jwoo jwaa / chwii chwoo chwaa

store(MShortVowels) 'ᒥᒧᒪ' c mi mo ma
store(MLongVowels) 'ᒦᒨᒫ' c mii moo maa
store(MWShortVowels) 'ᒯᒳᒷ' c mwi mwo mwa
store(MWLongVowels) 'ᒱᒵᒹ' c mwii mwoo mwaa

store(NShortVowels) 'ᓂᓄᓇ' c ni no na
store(NLongVowels) 'ᓃᓅᓈ' c nii noo naa

store(SZShortVowels) 'ᓯᓱᓴ' c si so sa / zi zo za
store(SZLongVowels) 'ᓰᓲᓵ' c sii soo saa / zii zoo zaa
store(SZWShortVowels) 'ᓹᓽᔁ' c swi swo swa / zwi zwo zwa
store(SZWLongVowels) 'ᓻᓿᔃ' c swii swoo swaa / zwii zwoo zwaa

store(SHZHShortVowels) 'ᔑᔓᔕ' c shi sho sha / zhi zho zha 
store(SHZHLongVowels) 'ᔒᔔᔖ' c shii shoo shaa / zhii zhoo zhaa
store(SHZHWShortVowels) 'ᔚᔞᔢ' c shwi shwo shwa / zhwi zhwo zhwa
store(SHZHWLongVowels) 'ᔜᔠᔤ' c shwii shwoo shwaa / zhwii zhwoo zhwaa

store(YShortVowels) 'ᔨᔪᔭ' c yi yo ya
store(YLongVowels) 'ᔩᔫᔮ' c yii yoo yaa
store(YWShortVowels) 'ᔲᔶᔺ' c ywi ywo ywa
store(YWLongVowels) 'ᔴᔸᔼ' c ywii ywoo ywaa

c Standalone Vowels

+ 'e' > 'ᐁ'
+ 'i' > 'ᐃ'
'ᐃ' + [K_I] > 'ᐄ'
+ 'o' > 'ᐅ'
'ᐅ' + [K_O] > 'ᐆ'
+ 'a' > 'ᐊ'
'ᐊ' + [K_A] > 'ᐋ'
any(ShortVowels) + [K_W] > index(WShortVowels,1)
any(LongVowels) + [K_W] > index(WLongVowels,1)

c Standalone Consonants

+ 'h' > 'ᐦ'
+ [SHIFT K_L] > 'ᓬ'
+ [SHIFT K_R] > 'ᕒ'


c Syllable Building
+ [K_B] > 'ᑉ'
+ [K_P] > 'ᑉ'
'ᑉ' + [K_E] > 'ᐯ'
'ᑉ' + any(VowelKeys) > index(BPShortVowels,2)
any(BPShortVowels) + any(VowelKeys) > index(BPLongVowels,1)
any(BPLongVowels) + [K_BKSP] > index(BPShortVowels,1)
'ᑉ' + [K_W] > 'ᐻ'
'ᐻ' + [K_E] > 'ᐻ'
'ᐻ' + any(VowelKeys) > index(BPWShortVowels,2)
any(BPWShortVowels) + any(VowelKeys) > index(BPWLongVowels,1)
any(BPWLongVowels) + [K_BKSP] > index(BPWShortVowels,1)

+ [K_D] > 'ᑦ'
+ [K_T] > 'ᑦ'
'ᑦ' + [K_E] > 'ᑌ'
'ᑦ' + any(VowelKeys) > index(DTShortVowels,2)
any(DTShortVowels) + any(VowelKeys) > index(DTLongVowels,1)
any(DTLongVowels) + [K_BKSP] > index(DTShortVowels,1)
'ᑦ' + [K_W] > 'ᑘ'
'ᑘ' + [K_E] > 'ᑘ'
'ᑘ' + any(VowelKeys) > index(DTWShortVowels,2)
any(DTWShortVowels) + any(VowelKeys) > index(DTWLongVowels,1)
any(DTWLongVowels) + [K_BKSP] > index(DTWShortVowels,1)

+ [K_G] > 'ᒃ'
+ [K_K] > 'ᒃ'
'ᒃ' + [K_E] > 'ᑫ'
'ᒃ' + any(VowelKeys) > index(GKShortVowels,2)
any(GKShortVowels) + any(VowelKeys) > index(GKLongVowels,1)
any(GKLongVowels) + [K_BKSP] > index(GKShortVowels,1)
'ᒃ' + [K_W] > 'ᑵ'
'ᑵ' + [K_E] > 'ᑵ'
'ᑵ' + any(VowelKeys) > index(GKWShortVowels,2)
any(GKWShortVowels) + any(VowelKeys) > index(GKWLongVowels,1)
any(GKWLongVowels) + [K_BKSP] > index(GKWShortVowels,1)

+ [K_J] > 'ᒡ'
'c' + [K_H] > 'ᒡ'
'ᒡ' + [K_E] > 'ᒉ'
'ᒡ' + any(VowelKeys) > index(JCHShortVowels,2)
any(JCHShortVowels) + any(VowelKeys) > index(JCHLongVowels,1)
any(JCHLongVowels) + [K_BKSP] > index(JCHShortVowels,1)
'ᒡ' + [K_W] > 'ᒓ'
'ᒓ' + [K_E] > 'ᒓ'
'ᒓ' + any(VowelKeys) > index(JCHWShortVowels,2)
any(JCHWShortVowels) + any(VowelKeys) > index(JCHWLongVowels,1)
any(JCHWLongVowels) + [K_BKSP] > index(JCHWShortVowels,1)

+ [K_M] > 'ᒻ'
'ᒻ' + [K_E] > 'ᒣ'
'ᒻ' + any(VowelKeys) > index(MShortVowels,2)
any(MShortVowels) + any(VowelKeys) > index(MLongVowels,1)
any(MLongVowels) + [K_BKSP] > index(MShortVowels,1)
'ᒻ' + [K_W] > 'ᒭ'
'ᒭ' + [K_E] > 'ᒭ'
'ᒭ' + any(VowelKeys) > index(MWShortVowels,2)
any(MWShortVowels) + any(VowelKeys) > index(MWLongVowels,1)
any(MWLongVowels) + [K_BKSP] > index(MWShortVowels,1)

+ [K_N] > 'ᓐ'
'ᓐ' + [K_E] > 'ᓀ'
'ᓐ' + any(VowelKeys) > index(NShortVowels,2)
any(NShortVowels) + any(VowelKeys) > index(NLongVowels,1)
any(NLongVowels) + [K_BKSP] > index(NShortVowels,1)
'ᓐ' + [K_W] > 'ᓊ'
'ᓊ' + [K_E] > 'ᓊ'
'ᓊ' + [K_A] > 'ᓌ' 
'ᓌ' + [K_A] > 'ᓎ' 
'ᓎ' + [K_BKSP] > 'ᓌ'

+ [K_L] > 'ᓫ'
'ᓫ' + [K_E] > 'ᓀᓫ'
'ᓫ' + any(VowelKeys) > index(NShortVowels,2)'ᓫ'
'ᓂᓫ' + [K_I] > 'ᓃᓫ'
'ᓄᓫ' + [K_O] > 'ᓅᓫ'
'ᓇᓫ' + [K_A] > 'ᓈᓫ'
'ᓃᓫ' + [K_BKSP] > 'ᓂᓫ'
'ᓅᓫ' + [K_BKSP] > 'ᓄᓫ'
'ᓈᓫ' + [K_BKSP] > 'ᓇᓫ'

+ [K_R] > 'ᕑ'
'ᕑ' + [K_E] > 'ᓀᕑ'
'ᕑ' + any(VowelKeys) > index(NShortVowels,2)'ᕑ'
'ᓂᕑ' + [K_I] > 'ᓃᕑ'
'ᓄᕑ' + [K_O] > 'ᓅᕑ'
'ᓇᕑ' + [K_A] > 'ᓈᕑ'
'ᓃᕑ' + [K_BKSP] > 'ᓂᕑ'
'ᓅᕑ' + [K_BKSP] > 'ᓄᕑ'
'ᓈᕑ' + [K_BKSP] > 'ᓇᕑ'

+ [K_S] > 'ᔅ'
+ [K_Z] > 'ᔅ'
'ᔅ' + [K_E] > 'ᓭ'
'ᔅ' + any(VowelKeys) > index(SZShortVowels,2)
any(SZShortVowels) + any(VowelKeys) > index(SZLongVowels,1)
any(SZLongVowels) + [K_BKSP] > index(SZShortVowels,1)
'ᔅ' + [K_W] > 'ᓷ'
'ᓷ' + [K_E] > 'ᓷ'
'ᓷ' + any(VowelKeys) > index(SZWShortVowels,2)
any(SZWShortVowels) + any(VowelKeys) > index(SZWLongVowels,1)
any(SZWLongVowels) + [K_BKSP] > index(SZWShortVowels,1)

'ᔅ' + [K_H] > 'ᔥ'
'ᔥ' + [K_E] > 'ᔐ'
'ᔥ' + any(VowelKeys) > index(SHZHShortVowels,2)
any(SHZHShortVowels) + any(VowelKeys) > index(SHZHLongVowels,1)
any(SHZHLongVowels) + [K_BKSP] > index(SHZHShortVowels,1)
'ᔥ' + [K_W] > 'ᔘ'
'ᔘ' + [K_E] > 'ᔘ'
'ᔘ' + any(VowelKeys) > index(SHZHWShortVowels,2)
any(SHZHWShortVowels) + any(VowelKeys) > index(SHZHWLongVowels,1)
any(SHZHWLongVowels) + [K_BKSP] > index(SHZHWShortVowels,1)

+ [K_Y] > 'ᔾ'
'ᔾ' + [K_E] > 'ᔦ'
'ᔾ' + any(VowelKeys) > index(YShortVowels,2)
any(YShortVowels) + any(VowelKeys) > index(YLongVowels,1)
any(YLongVowels) + [K_BKSP] > index(YShortVowels,1)
'ᔾ' + [K_W] > 'ᔰ'
'ᔰ' + [K_E] > 'ᔰ'
'ᔰ' + any(VowelKeys) > index(YWShortVowels,2)
any(YWShortVowels) + any(VowelKeys) > index(YWLongVowels,1)
any(YWLongVowels) + [K_BKSP] > index(YWShortVowels,1)

+ [K_W] > 'ᐤ'
+ [SHIFT K_W] > 'ᣝ'
'ᐤ' + [K_E] > 'ᐍ'
'ᐤ' + any(VowelKeys) > index(WShortVowels,2)
any(WShortVowels) + any(VowelKeys) > index(WLongVowels,1)
any(WLongVowels) + [K_BKSP] > index(WShortVowels,1)

c punctuation
+ '.' > '᙮'
+ [K_LBRKT] > deadkey(Deadkey) 
