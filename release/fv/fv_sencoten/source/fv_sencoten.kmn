store(&VERSION) "10.0"
store(&KEYBOARDVERSION) '9.2'
store(&TARGETS) "any"
c store(&ETHNOLOGUECODE) "str"
store(&COPYRIGHT) '(c) 2008-2022 FirstVoices, SIL International. Portions (c) 2006 Chris Harvey'
store(&NAME) 'SENĆOŦEN'
store(&BITMAP) 'fv_sencoten.ico'
store(&HOTKEY) '[CTRL SHIFT K_6]'
store(&VISUALKEYBOARD) 'fv_sencoten.kvks'
store(&KMW_HELPFILE) 'fv_sencoten_kmw_help.htm'
 
begin Unicode > use(main)                      
group(main) using keys  

c --- START OF TOUCH LAYOUT RULES ---

c Touch layout rules originally imported from KeyMap.plist on Tue Nov 17 2015

store(&LAYOUTFILE) 'fv_sencoten.keyman-touch-layout'

+ [T_Q] > 'Q'
+ [T_W_0] > 'W̱'
+ [T_W] > 'W'
+ [T_E] > 'E'
'N' + [T_R] > 'Ṉ'
'W' + [T_R] > 'W̱'
'K' + [T_R] > 'Ḵ'
'T' + [T_R] > 'Ṯ'
'X' + [T_R] > 'X̱'
+ [T_T_0] > 'Ṯ'
+ [T_T_1] > 'Ⱦ'
+ [T_T_2] > 'Ŧ'
+ [T_T] > 'T'
+ [T_Y] > 'Y'
+ [T_U] > 'U'
+ [T_I_0] > 'Í'
+ [T_I] > 'I'
+ [T_O] > 'O'
+ [T_P] > 'P'
+ [T_A_0] > 'Á'
+ [T_A_1] > 'Ⱥ'
+ [T_A] > 'A'
+ [T_S_0] > 'Ś'
+ [T_S] > 'S'
+ [T_D] > 'D'
'A' + [T_F] > 'Ⱥ'
'T' + [T_F] > 'Ⱦ'
'C' + [T_F] > 'Ȼ'
'K' + [T_G] > '₭'
'T' + [T_G] > 'Ŧ'
'L' + [T_G] > 'Ƚ'
+ [T_H] > 'H'
+ [T_J] > 'J'
+ [T_K_0] > 'Ḵ'
+ [T_K_1] > 'Ḱ'
+ [T_K_2] > '₭'
+ [T_K] > 'K'
+ [T_L_0] > 'Ƚ'
+ [T_L] > 'L'
+ [T_COLON] > U+00B8
+ [T_Z] > 's'
+ [T_X] > 'Z'
+ [T_C_0] > 'X̱'
+ [T_C] > 'X'
+ [T_V_0] > 'Ć'
+ [T_V_1] > 'Ȼ'
+ [T_V] > 'C'
'I' + [T_B] > 'Í'
'A' + [T_B] > 'Á'
'S' + [T_B] > 'Ś'
'K' + [T_B] > 'Ḱ'
'C' + [T_B] > 'Ć'
+ [T_N] > 'B'
+ [T_M_0] > 'Ṉ'
+ [T_M] > 'N'
+ [T_COMMA] > 'M'

c --- END OF TOUCH LAYOUT RULES ---

store(&CasedKeys) 's' c in capslock, shift S should un-caps into s
+ 's' > 's'
+ 'S' > 'S'

+ [K_COMMA] > '¸' c cedilla
+ [SHIFT K_COMMA] > ','
+ [SHIFT K_PERIOD] > ';'

store(acu0) 'ACIKS'
store(acu1) 'ÁĆÍḰŚ'   
store(sla0) 'ACT' 
store(sla1) "ȺȻȾ"
store(bar0) 'KTL'
store(bar1) '₭ŦȽ' 
store(mau0) 'KNT'
store(mau1) 'ḴṈṮ'
store(mau2) 'WX'  

any(sla0) + [K_SLASH] > index(sla1,1)
any(acu0) + [K_QUOTE] > index(acu1,1)
any(bar0) + [K_HYPHEN] > index(bar1,1) 
any(mau0) + [K_COLON] > index(mau1,1)
any(mau2) + [K_COLON] > index(mau2,1) U+0331  

store(quote0) "[]{}"
store(quote1) "«»‹›"
+ any(quote0) > index(quote1,1) 

store(original2) "/'-;,[]{}<>" 
store(original1) [RALT K_SLASH] [RALT K_QUOTE] [RALT K_HYPHEN] [RALT K_COLON] [RALT K_COMMA] [RALT K_LBRKT] [RALT K_RBRKT] [SHIFT RALT K_LBRKT] [SHIFT RALT K_RBRKT] [SHIFT RALT K_COMMA] [SHIFT RALT K_PERIOD]
+ any(original1) > index(original2,1)  



match > use(normalize)
nomatch > use(x)

group(normalize)

group(x) using keys

store(allkeys) 'abcdefghijklmnopqrstuvwxyzABCDEFGHIJKLMNOPQRSTUVWXYZ1234567890-=[]\;,./`~!@#$%^&*()_+{}|:"<>?' "'"
+ any(allkeys) > index(allkeys, 1)
