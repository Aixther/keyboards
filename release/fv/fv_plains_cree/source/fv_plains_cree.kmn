store(&VERSION) "10.0"
store(&TARGETS) 'any mobile tablet'
store(&COPYRIGHT) '(c) 2015-2023 FirstVoices, SIL International, 2015 First Peoples' U+0027 ' Cultural Foundation'
store(&NAME) 'ᓀᐦᐃᔭᐍᐏᐣ'
store(&KEYBOARDVERSION) '11.0'
store(&LAYOUTFILE) 'fv_plains_cree.keyman-touch-layout'
store(&VISUALKEYBOARD) 'fv_plains_cree.kvks'

begin Unicode > use(main)

group(main) using keys

c --- START OF DESKTOP RULES ---
store(ShortVowelsOnly) 'ᐁᐃᐅᐊ' c e i o a
store(LongVowelsOnly) 'ᐁᐄᐆᐋ' c e ii oo aa
store(VowelKeys) [K_E] [K_I] [K_O] [K_A]

store(WShortVowels) 'ᐍᐏᐓᐘ' c we wi wo wa
store(WLongVowels) 'ᐍᐑᐕᐚ' c we wii woo waa

+ 'e' > 'ᐁ'
+ 'i' > 'ᐃ'
'ᐃ' + [K_I] > 'ᐄ' 
+ 'o' > 'ᐅ'
'ᐅ' + [K_O] > 'ᐆ'
+ 'a' > 'ᐊ'
'ᐊ' + [K_A] > 'ᐋ'

any(LongVowelsOnly) + [K_BKSP] > index(ShortVowelsOnly,1)

+'w' > 'ᐤ'
'ᐤ' + any(VowelKeys) > index(WShortVowels,2)
any(WShortVowels) + any(VowelKeys) > index(WLongVowels,1)
any(WShortVowels) + [K_BKSP] > 'ᐤ'
any(WLongVowels) + [K_BKSP] > index(WShortVowels,1)





c --- START OF TOUCH LAYOUT RULES ---

store(withoutW) 'ᐁᐃᐄᐅᐆᐊᐋᐯᐱᐲᐳᐴᐸᐹᑌᑎᑏᑐᑑᑕᑖᑫᑭᑮᑯᑰᑲᑳᒉᒋᒌᒍᒎᒐᒑᒣᒥᒦᒧᒨᒪᒫᓀᓇᓈᓭᓯᓰᓱᓲᓴᓵᔦᔨᔩᔪᔫᔭᔮ'
store(withW) 'ᐍᐏᐑᐓᐕᐘᐚᐻᐽᐿᑁᑃᑅᑇᑘᑚᑜᑞᑠᑢᑤᑵᑷᑹᑻᑽᑿᒁᒓᒕᒗᒙᒛᒝᒟᒭᒯᒱᒳᒵᒷᒹᓊᓌᓎᓷᓹᓻᓽᓿᔁᔃᔰᔲᔴᔶᔸᔺᔼ'
store(withoutLong) 'ᐃᐅᐊᐏᐓᐘᐱᐳᐸᐽᑁᑅᑎᑐᑕᑚᑞᑢᑭᑯᑲᑷᑻᑿᒋᒍᒐᒕᒙᒝᒥᒧᒪᒯᒳᒷᓂᓄᓇᓌᓯᓱᓴᓹᓽᔁᔨᔪᔭᔲᔶᔺ'
store(withLong) 'ᐄᐆᐋᐑᐕᐚᐲᐴᐹᐿᑃᑇᑏᑑᑖᑜᑠᑤᑮᑰᑳᑹᑽᒁᒌᒎᒑᒗᒛᒟᒦᒨᒫᒱᒵᒹᓃᓅᓈᓎᓰᓲᓵᓻᓿᔃᔩᔫᔮᔴᔸᔼ'

  any(withoutW) + [T_W_DOT] > index(withW, 1)
  any(withoutLong) + [T_LONG_DOT] > index(withLong, 1)


c --- END OF TOUCH LAYOUT RULES ---

