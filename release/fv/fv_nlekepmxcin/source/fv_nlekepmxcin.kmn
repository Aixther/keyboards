store(&VERSION) "10.0"
store(&KEYBOARDVERSION) '9.2.4'
store(&TARGETS) "any"
c store(&ETHNOLOGUECODE) "thp"
store(&COPYRIGHT) '(c) 2008-2023 FirstVoices, SIL International. Portions (c) 2006 Chris Harvey'
store(&NAME) 'Nłeʔkepmxcin'
store(&BITMAP) 'fv_nlekepmxcin.ico'
store(&VISUALKEYBOARD) 'fv_nlekepmxcin.kvks'
store(&KMW_HELPFILE) 'fv_nlekepmxcin_kmw_help.htm'
 
begin Unicode > use(main)                      
group(main) using keys


c --- START OF TOUCH LAYOUT RULES ---

c Touch layout rules originally imported from KeyMap.plist on Tue Nov 17 2015

store(&LAYOUTFILE) 'fv_nlekepmxcin.keyman-touch-layout'

+ [SHIFT T_Q_0] > 'Q̓' layer('default')
+ [SHIFT T_Q] > 'Q' layer('default')
+ [SHIFT T_W_0] > 'W̓' layer('default')
+ [SHIFT T_W] > 'W' layer('default')
+ [SHIFT T_E_0] > 'É' layer('default')
+ [SHIFT T_E] > 'E' layer('default')
'c' + [SHIFT T_R] > 'c̓'
'k' + [SHIFT T_R] > 'k̓'
'l' + [SHIFT T_R] > 'l̓'
'm' + [SHIFT T_R] > 'm̓'
'n' + [SHIFT T_R] > 'n̓'
'p' + [SHIFT T_R] > 'p̓'
'q' + [SHIFT T_R] > 'q̓'
't' + [SHIFT T_R] > 't̓'
'w' + [SHIFT T_R] > 'w̓'
'y' + [SHIFT T_R] > 'y̓'
'z' + [SHIFT T_R] > 'z̓'
'ɣ' + [SHIFT T_R] > 'ɣ̓'
'C' + [SHIFT T_R] > 'C̓'
'K' + [SHIFT T_R] > 'K̓'
'L' + [SHIFT T_R] > 'L̓'
'M' + [SHIFT T_R] > 'M̓'
'N' + [SHIFT T_R] > 'N̓'
'P' + [SHIFT T_R] > 'P̓'
'Q' + [SHIFT T_R] > 'Q̓'
'T' + [SHIFT T_R] > 'T̓'
'W' + [SHIFT T_R] > 'W̓'
'Y' + [SHIFT T_R] > 'Y̓'
'Z' + [SHIFT T_R] > 'Z̓'
'Ɣ' + [SHIFT T_R] > 'Ɣ̓'
'ʕ' + [SHIFT T_R] > 'ʕ̓'
'ƛ̓' + [SHIFT T_R] > 'ƛ̓'
+ [SHIFT T_T_0] > 'T̓' layer('default')
+ [SHIFT T_T] > 'T' layer('default')
+ [SHIFT T_Y_0] > 'Y̓' layer('default')
+ [SHIFT T_Y] > 'Y' layer('default')
+ [SHIFT T_U_0] > 'Ú' layer('default')
+ [SHIFT T_U] > 'U' layer('default')
+ [SHIFT T_I_0] > 'Í' layer('default')
+ [SHIFT T_I_1] > 'Ị' layer('default')
+ [SHIFT T_I_2] > U+0049 U+0323 U+0301 layer('default')
+ [SHIFT T_I] > 'I' layer('default')
+ [SHIFT T_O_0] > 'Ó' layer('default')
+ [SHIFT T_O] > 'O' layer('default')
+ [SHIFT T_P_0] > 'P̓' layer('default')
+ [SHIFT T_P] > 'P' layer('default')
+ [SHIFT T_A_0] > 'Á' layer('default')
+ [SHIFT T_A] > 'A' layer('default')
+ [SHIFT T_S_0] > 'Ṣ' layer('default')
+ [SHIFT T_S] > 'S' layer('default')
+ [SHIFT T_D] > 'ʔ' layer('default')
+ [SHIFT T_F] > 'ƛ̓' layer('default')
+ [SHIFT T_G_0] > 'Ɣ̓' layer('default')
+ [SHIFT T_G] > 'Ɣ' layer('default')
+ [SHIFT T_H] > 'H' layer('default')
+ [SHIFT T_J] > 'ᵂ' layer('default')
+ [SHIFT T_K_0] > 'K̓' layer('default')
+ [SHIFT T_K] > 'K' layer('default')
+ [SHIFT T_L_0] > 'L̓' layer('default')
+ [SHIFT T_L_1] > 'Ḷ' layer('default')
+ [SHIFT T_L] > 'L' layer('default')
+ [SHIFT T_COLON] > 'Ł' layer('default')
+ [SHIFT T_Z_0] > 'Z̓' layer('default')
+ [SHIFT T_Z_1] > 'Θ' layer('default')
+ [SHIFT T_Z_2] > 'Θ̓' layer('default')
+ [SHIFT T_Z] > 'Z' layer('default')
+ [SHIFT T_X_0] > 'X̣' layer('default')
+ [SHIFT T_X] > 'X' layer('default')
+ [SHIFT T_C_0] > 'C̓' layer('default')
+ [SHIFT T_C_1] > 'C̣' layer('default')
+ [SHIFT T_C] > 'C' layer('default')
'c' + [SHIFT T_V] > 'c̣'
'ə' + [SHIFT T_V] > 'ə̣'
'ə́' + [SHIFT T_V] > 'ə̣́'
'i' + [SHIFT T_V] > 'ị'
'í' + [SHIFT T_V] > U+00ED U+0323 c 'ị́'
'l' + [SHIFT T_V] > 'ḷ'
's' + [SHIFT T_V] > 'ṣ'
'x' + [SHIFT T_V] > 'x̣'
'C' + [SHIFT T_V] > 'C̣'
'Ə' + [SHIFT T_V] > 'Ə̣'
'Ə́' + [SHIFT T_V] > 'Ə̣́'
'I' + [SHIFT T_V] > 'Ị'
'Í' + [SHIFT T_V] > U+00CD U+0323 c 'Ị́'
'L' + [SHIFT T_V] > 'Ḷ'
'S' + [SHIFT T_V] > 'Ṣ'
'X' + [SHIFT T_V] > 'X̣'
+ [SHIFT T_B_0] > 'ʕ̓' layer('default')
+ [SHIFT T_B] > 'ʕ' layer('default')
+ [SHIFT T_N_0] > 'N̓' layer('default')
+ [SHIFT T_N] > 'N' layer('default')
+ [SHIFT T_M_0] > 'M̓' layer('default')
+ [SHIFT T_M] > 'M' layer('default')
+ [SHIFT T_COMMA_0] > 'Ə́' layer('default')
+ [SHIFT T_COMMA_1] > 'Ə̣' layer('default')
+ [SHIFT T_COMMA_2] > U+018F U+0323 U+0301 layer('default') c 'Ə̣́'
+ [SHIFT T_COMMA] > 'Ə' layer('default')
+ [T_Q_0] > 'q̓'
+ [T_Q] > 'q'
+ [T_W_0] > 'w̓'
+ [T_W] > 'w'
+ [T_E_0] > 'é'
+ [T_E] > 'e'
'c' + [T_R] > 'c̓'
'k' + [T_R] > 'k̓'
'l' + [T_R] > 'l̓'
'm' + [T_R] > 'm̓'
'n' + [T_R] > 'n̓'
'p' + [T_R] > 'p̓'
'q' + [T_R] > 'q̓'
't' + [T_R] > 't̓'
'w' + [T_R] > 'w̓'
'y' + [T_R] > 'y̓'
'z' + [T_R] > 'z̓'
'ɣ' + [T_R] > 'ɣ̓'
'C' + [T_R] > 'C̓'
'K' + [T_R] > 'K̓'
'L' + [T_R] > 'L̓'
'M' + [T_R] > 'M̓'
'N' + [T_R] > 'N̓'
'P' + [T_R] > 'P̓'
'Q' + [T_R] > 'Q̓'
'T' + [T_R] > 'T̓'
'W' + [T_R] > 'W̓'
'Y' + [T_R] > 'Y̓'
'Z' + [T_R] > 'Z̓'
'Ɣ' + [T_R] > 'Ɣ̓'
'ʕ' + [T_R] > 'ʕ̓'
'ƛ̓' + [T_R] > 'ƛ̓'
+ [T_T_0] > 't̓'
+ [T_T] > 't'
+ [T_Y_0] > 'y̓'
+ [T_Y] > 'y'
+ [T_U_0] > 'ú'
+ [T_U] > 'u'
+ [T_I_0] > 'í'
+ [T_I_1] > 'ị'
+ [T_I_2] > U+00ED U+0323 c 'ị́'
+ [T_I] > 'i'
+ [T_O_0] > 'ó'
+ [T_O] > 'o'
+ [T_P_0] > 'p̓'
+ [T_P] > 'p'
+ [T_A_0] > 'á'
+ [T_A] > 'a'
+ [T_S_0] > 'ṣ'
+ [T_S] > 's'
+ [T_D] > 'ʔ'
+ [T_F] > 'ƛ̓'
+ [T_G_0] > 'ɣ̓'
+ [T_G] > 'ɣ'
+ [T_H] > 'h'
+ [T_J] > 'ʷ'
+ [T_K_0] > 'k̓'
+ [T_K] > 'k'
+ [T_L_0] > 'l̓'
+ [T_L_1] > 'ḷ'
+ [T_L] > 'l'
+ [T_COLON] > 'ł'
+ [T_Z_0] > 'z̓'
+ [T_Z_1] > 'θ'
+ [T_Z_2] > 'θ̓'
+ [T_Z] > 'z'
+ [T_X_0] > 'x̣'
+ [T_X] > 'x'
+ [T_C_0] > 'c̓'
+ [T_C_1] > 'c̣'
+ [T_C] > 'c'
'c' + [T_V] > 'c̣'
'ə' + [T_V] > 'ə̣'
'ə́' + [T_V] > 'ə̣́'
'i' + [T_V] > 'ị'
'í' + [T_V] > U+00ED U+0323 c 'ị́'
'l' + [T_V] > 'ḷ'
's' + [T_V] > 'ṣ'
'x' + [T_V] > 'x̣'
'C' + [T_V] > 'C̣'
'Ə' + [T_V] > 'Ə̣'
'Ə́' + [T_V] > 'Ə̣́'
'I' + [T_V] > 'Ị'
'Í' + [T_V] > U+00CD U+0323  c 'Ị́'
'L' + [T_V] > 'Ḷ'
'S' + [T_V] > 'Ṣ'
'X' + [T_V] > 'X̣'
+ [T_B_0] > 'ʕ̓'
+ [T_B] > 'ʕ'
+ [T_N_0] > 'n̓'
+ [T_N] > 'n'
+ [T_M_0] > 'm̓'
+ [T_M] > 'm'
+ [T_COMMA_0] > 'é'
+ [T_COMMA_1] > 'ə̣'
+ [T_COMMA_2] > 'ə̣́'
+ [T_COMMA] > 'ə'
'ə' + [T_AC] > 'ə́'
'ə̣' + [T_AC] > 'ə̣́'
'Ə' + [T_AC] > 'Ə́'
'Ə̣' + [T_AC] > 'Ə̣́'
'ə' + [SHIFT T_AC] > 'ə́'
'ə̣' + [SHIFT T_AC] > 'ə̣́'
'Ə' + [SHIFT T_AC] > 'Ə́'
'Ə̣' + [SHIFT T_AC] > 'Ə̣́'

c Backspace
store(Backspace) [K_BKSP]
any(HasAcute) + any(Backspace) > index(NeedsAcute,1)

c Touch Acute Rules
store(TouchAcuteKey) [T_AC] [SHIFT T_AC]
any(NeedsAcute) + any(TouchAcuteKey) > index(HasAcute,1)

c --- END OF TOUCH LAYOUT RULES ---

c *************
c constraints
c *************

store(topaccent)  U+0300 U+0301 U+0313
store(topaccentkey) "`;'"          

any(topaccent) + any(topaccentkey) > context beep

store(lowaccent) U+0323 
any(lowaccent) + "1" > context beep

c Acute rules
store(NeedsAcute) 'AaEeIiOoUu'
store(HasAcute) 'ÁáÉéÍíÓóÚú'
store(Acute) U+0301
store(AcuteKeys) [K_COLON]
any(NeedsAcute) + any(AcuteKeys) > index(HasAcute,1)
any(HasAcute) + any(AcuteKeys) > index(HasAcute,1)
any(Acute) + any(AcuteKeys) > index(HasAcute,1)
                  
store(NumbersAndPunctuation) '1234567890-=[]\;,./`~!@#$%^&*()_+{}|:"<>?' "'"  c preventing accents on non-letters
any(NumbersAndPunctuation) + any(topaccentkey) > context beep                   
any(NumbersAndPunctuation) + "1" > context beep

c original values
store(orig0) [RALT K_1] [NCAPS RALT K_2] [NCAPS RALT K_3] [RALT K_4] [NCAPS RALT K_5] [NCAPS RALT K_6] [NCAPS RALT K_7] [NCAPS RALT K_8] [NCAPS RALT K_9] [RALT K_0] [RALT K_LBRKT] [RALT K_RBRKT] [RALT K_QUOTE] [RALT K_COLON] [RALT K_COMMA] [RALT K_PERIOD]
store(orig1) "1234567890[]';<>"            
store(punc0) [SHIFT RALT K_1] [NCAPS SHIFT RALT K_2] [NCAPS SHIFT RALT K_3] [SHIFT RALT K_4] [NCAPS SHIFT RALT K_5] [NCAPS SHIFT RALT K_6] [NCAPS SHIFT RALT K_7] [NCAPS SHIFT RALT K_8] [NCAPS SHIFT RALT K_9] [SHIFT RALT K_0] [SHIFT RALT K_LBRKT] [SHIFT RALT K_RBRKT] 
store(punc1) "!@#$%^&*(){}"
+ any(orig0) > index(orig1,1)
+ any(punc0) > index(punc1,1)

c non-caps modified keys
  
store(Mod0) "`~140)[]{}';<>" [SHIFT RALT K_HYPHEN] [SHIFT RALT K_EQUAL] [RALT K_HYPHEN] [RALT K_EQUAL] 
store(Mod1) U+0300 "·" U+0323 "ʔʕ¢‘’“”" U+0313 U+0301 "()‹›«»"      
+ any(Mod0) > index(Mod1,1)   

c glottaled
store(MiniGlot0) [CAPS SHIFT K_8] [NCAPS K_8]  
store(MiniGlot1)  "ƛƛ"
store(MajGlot0)  [NCAPS SHIFT K_8] [CAPS K_8] 
store(MajGlot1) U+F899 U+F899
+ any(MiniGlot0) > index(MiniGlot1,1) "̓"
+ any(MajGlot0) > index(MajGlot1,1) "̓"           

c dotted
store(MiniDot0) [CAPS SHIFT K_7] [NCAPS K_7]  
store(MiniDot1)  "xx"
store(MajDot0)   [NCAPS SHIFT K_7]  [CAPS K_7] 
store(MajDot1) "XX"             
+ any(MiniDot0) > index(MiniDot1,1) U+0323        
+ any(MajDot0) > index(MajDot1,1) U+0323   

c other characters
store(MiniOth0) [CAPS SHIFT K_2] [CAPS SHIFT K_3] [CAPS SHIFT K_5]  [CAPS SHIFT K_6] [CAPS SHIFT K_9] [NCAPS K_2]  [NCAPS K_3] [NCAPS K_5]  [NCAPS K_6]  [NCAPS K_9] 
store(MiniOth1)  "ʷəɣθłʷəɣθł"
store(MajOth0)  [NCAPS SHIFT K_2] [NCAPS SHIFT K_3] [NCAPS SHIFT K_5] [NCAPS SHIFT K_6] [NCAPS SHIFT K_9] [CAPS K_2] [CAPS K_3]  [CAPS K_5]  [CAPS K_6]  [CAPS K_9] 
store(MajOth1) "ᵂƏƔΘŁᵂƏƔΘŁ"             
+ any(MiniOth0) > index(MiniOth1,1)       
+ any(MajOth0) > index(MajOth1,1)   

match > use(normalize)
nomatch > use(x)

group(normalize)

any(topaccent) any(lowaccent) > context(2) context(1)


group(x) using keys

